<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://notes.eatonphil.com/2023-06-19-metaprogramming-in-zig-and-parsing-css.html">Original</a>
    <h1>Metaprogramming in Zig and parsing CSS</h1>
    
    <div id="readability-page-1" class="page"><div>
      <div>
        <p>I knew Zig supported some sort of reflection on types. But I had been
confused about how to use it. What&#39;s the difference between
<code>@typeInfo</code> and <code>@TypeOf</code>? I ignored this aspect of Zig until a
problem came up at <a href="https://tigerbeetle.com">work</a> where reflection
made sense.</p>
<p>The situation was parsing and storing parsed fields in a struct. Each
field name that is parsed should match up to a struct field.</p>
<p>This is a fairly common problem. So this post walks through how to use
Zig&#39;s metaprogramming features in a simpler but related domain:
parsing CSS into typed objects, and pretty-printing these typed CSS
objects.</p>
<p>I live-streamed the implementation of this project yesterday on
<a href="https://www.twitch.tv/eatonphil">Twitch</a>. The video is <a href="https://youtube.com/@eatonphil">available on
YouTube</a>. And the source is <a href="https://github.com/eatonphil/zig-metaprogramming-css-parser">available
on GitHub</a>.</p>
<p>If you want to skip the parsing steps and just see the
metaprogramming, jump to the implementation of
<a href="#&lt;code&gt;match_property&lt;/code&gt;">match_property</a>.</p>
<h3 id="parsing-css">Parsing CSS</h3><p>Let&#39;s imagine a CSS that only has alphabetical selectors, property
names and values.</p>
<p>The following would be valid:</p>
<pre><code>div {
  background: black;
  color: white;
}

a {
  color: blue;
}
</code></pre>
<p>Thinking about the structure of this stripped down CSS we&#39;ve got:</p>
<ol>
<li>CSS properties that consist of property names and values (in our case the property names are limited to <code>background</code> and <code>color</code>)</li>
<li>CSS rules that have a selector and a list of rules</li>
<li>CSS sheets that have a list of rules</li>
</ol>
<p>Turning that into Zig in <code>main.zig</code>:</p>
<pre><code>const std = @import(&#34;std&#34;);

const CSSProperty = union(enum) {
    unknown: void,
    color: []const u8,
    background: []const u8,
};

const CSSRule = struct {
    selector: []const u8,
    properties: []CSSProperty,
};

const CSSSheet = struct {
    rules: []CSSRule,
};
</code></pre>
<p>The parser is going to look for CSS rules which contain a selector and
a list of CSS rules. The entrypoint is that simple:</p>
<pre><code>fn parse(
    arena: *std.heap.ArenaAllocator,
    css: []const u8,
) !CSSSheet {
    var index: usize = 0;
    var rules = std.ArrayList(CSSRule).init(arena.allocator());

    // Parse rules until EOF.
    while (index &lt; css.len) {
        var res = try parse_rule(arena, css, index);
        index = res.index;
        try rules.append(res.rule);

        // In case there is trailing whitespace before the EOF,
        // eating whitespace here makes sure we exit the loop
        // immediately before trying to parse more rules.
        index = eat_whitespace(css, index);
    }

    return CSSSheet{
        .rules = rules.items,
    };
}
</code></pre>
<p>Let&#39;s implement the <code>eat_whitespace</code> helper we&#39;ve referenced. It
increments a cursor into the css file while it sees whitespace.</p>
<pre><code>fn eat_whitespace(
    css: []const u8,
    initial_index: usize,
) usize {
    var index = initial_index;
    while (index &lt; css.len and std.ascii.isWhitespace(css[index])) {
        index += 1;
    }

    return index;
}
</code></pre>
<p>In our stripped-down version of CSS all we have to think about is
ASCII. So the builtin <code>std.ascii.isWhitespace()</code> function is perfect.</p>
<p>Next, parsing CSS rules.</p>
<h4 id="&lt;code&gt;parse_rule()&lt;/code&gt;"><code>parse_rule()</code></h4><p>A rule consists of a selector, opening curly braces, any number of
properties, and closing curly braces. We need to remember to eat
whitespace between each piece of syntax.</p>
<p>And we&#39;ll reference a few more parsing helpers we&#39;ll talk about next
for the selector, braces, and properties.</p>
<pre><code>const ParseRuleResult = struct {
    rule: CSSRule,
    index: usize,
};
fn parse_rule(
    arena: *std.heap.ArenaAllocator,
    css: []const u8,
    initial_index: usize,
) !ParseRuleResult {
    var index = eat_whitespace(css, initial_index);

    // First parse selector(s).
    var selector_res = try parse_identifier(css, index);
    index = selector_res.index;

    index = eat_whitespace(css, index);

    // Then parse opening curly brace: {.
    index = try parse_syntax(css, index, &#39;{&#39;);

    index = eat_whitespace(css, index);

    var properties = std.ArrayList(CSSProperty).init(arena.allocator());
    // Then parse any number of properties.
    while (index &lt; css.len) {
        index = eat_whitespace(css, index);
        if (index &lt; css.len and css[index] == &#39;}&#39;) {
            break;
        }

        var attr_res = try parse_property(css, index);
        index = attr_res.index;

        try properties.append(attr_res.property);
    }

    index = eat_whitespace(css, index);

    // Then parse closing curly brace: }.
    index = try parse_syntax(css, index, &#39;}&#39;);

    return ParseRuleResult{
        .rule = CSSRule{
            .selector = selector_res.identifier,
            .properties = properties.items,
        },
        .index = index,
    };
}
</code></pre>
<p>The <code>parse_syntax</code> helper is pretty simple, it does a bounds check and
increments the cursor if the current character matches the one you
pass in.</p>
<pre><code>fn parse_syntax(
    css: []const u8,
    initial_index: usize,
    syntax: u8,
) !usize {
    if (initial_index &lt; css.len and css[initial_index] == syntax) {
        return initial_index + 1;
    }

    debug_at(css, initial_index, &#34;Expected syntax: &#39;{c}&#39;.&#34;, .{syntax});
    return error.NoSuchSyntax;
}
</code></pre>
<p>This calls attention to debugging messages on failure. When we
fail to parse a syntax, we want to give a useful error message and
point at the exact line and column of code where the error happens.</p>
<p>So let&#39;s implement <code>debug_at</code>.</p>
<h4 id="&lt;code&gt;debug_at&lt;/code&gt;"><code>debug_at</code></h4><p>First, we iterate over the entire CSS source code until we find the
entire line that contains the index where the parser failed. We also
want to identify the exact line and column corresponding to that
index.</p>
<pre><code>fn debug_at(
    css: []const u8,
    index: usize,
    comptime msg: []const u8,
    args: anytype,
) void {
    var line_no: usize = 1;
    var col_no: usize = 0;

    var i: usize = 0;
    var line_beginning: usize = 0;
    var found_line = false;
    while (i &lt; css.len) : (i += 1) {
        if (css[i] == &#39;\n&#39;) {
            if (!found_line) {
                col_no = 0;
                line_beginning = i;
                line_no += 1;
                continue;
            } else {
                break;
            }
        }

        if (i == index) {
            found_line = true;
        }

        if (!found_line) {
            col_no += 1;
        }
    }
</code></pre>
<p>Then we print it all out in a nice format for users (which will likely
just be ourselves).</p>
<pre><code>    std.debug.print(&#34;Error at line {}, column {}. &#34;, .{ line_no, col_no });
    std.debug.print(msg ++ &#34;\n\n&#34;, args);
    std.debug.print(&#34;{s}\n&#34;, .{css[line_beginning..i]});
    while (col_no &gt; 0) : (col_no -= 1) {
        std.debug.print(&#34; &#34;, .{});
    }
    std.debug.print(&#34;^ Near here.\n&#34;, .{});
}
</code></pre>
<p>Ok, popping our mental stack, if we look back at <code>parse_rule</code> we still
need to implement <code>parse_identifier</code> and <code>parse_property</code>.</p>
<h4 id="&lt;code&gt;parse_identifier&lt;/code&gt;"><code>parse_identifier</code></h4><p>An &#34;identifier&#34; for us here is just going to be an ASCII alphabetical
string (i.e. <code>[a-zA-Z]+</code>).  We&#39;re going to <em>really</em> simplify CSS
because we&#39;re going to use this method for parsing not just selectors
but property names and even property values.</p>
<p>Zig again has a nice builtin <code>std.ascii.isAlphabetical</code> we can use.</p>
<pre><code>const ParseIdentifierResult = struct {
    identifier: []const u8,
    index: usize,
};
fn parse_identifier(
    css: []const u8,
    initial_index: usize,
) !ParseIdentifierResult {
    var index = initial_index;
    while (index &lt; css.len and std.ascii.isAlphabetic(css[index])) {
        index += 1;
    }

    if (index == initial_index) {
        debug_at(css, initial_index, &#34;Expected valid identifier.&#34;, .{});
        return error.InvalidIdentifier;
    }

    return ParseIdentifierResult{
        .identifier = css[initial_index..index],
        .index = index,
    };
}
</code></pre>
<p>In reality, CSS properties are <a href="https://www.w3schools.com/cssref/css_selectors.php">highly
complex</a>. Parsing
CSS correctly isn&#39;t the main aim of this post though. :)</p>
<h4 id="&lt;code&gt;parse_property&lt;/code&gt;"><code>parse_property</code></h4><p>The final piece of CSS we need to parse is properties. These consist
of a property name, then a colon, then a property value, and finally a
semicolon. And within each piece we eat whitespace.</p>
<pre><code>const ParsePropertyResult = struct {
    property: CSSProperty,
    index: usize,
};
fn parse_property(
    css: []const u8,
    initial_index: usize,
) !ParsePropertyResult {
    var index = eat_whitespace(css, initial_index);

    // First parse property name.
    var name_res = parse_identifier(css, index) catch |e| {
        std.debug.print(&#34;Could not parse property name.\n&#34;, .{});
        return e;
    };
    index = name_res.index;

    index = eat_whitespace(css, index);

    // Then parse colon: :.
    index = try parse_syntax(css, index, &#39;:&#39;);

    index = eat_whitespace(css, index);

    // Then parse property value.
    var value_res = parse_identifier(css, index) catch |e| {
        std.debug.print(&#34;Could not parse property value.\n&#34;, .{});
        return e;
    };
    index = value_res.index;

    // Finally parse semi-colon: ;.
    index = try parse_syntax(css, index, &#39;;&#39;);

    var property = match_property(name_res.identifier, value_res.identifier) catch |e| {
        debug_at(css, initial_index, &#34;Unknown property: &#39;{s}&#39;.&#34;, .{name_res.identifier});
        return e;
    };

    return ParsePropertyResult{
        .property = property,
        .index = index,
    };
}
</code></pre>
<p>Finally we get to the first bit of metaprogramming. Once we have a
property name and value, we need to turn that into a Zig union.</p>
<p>That&#39;s what <code>match_property()</code> is going to be responsible for doing.</p>
<h3 id="&lt;code&gt;match_property&lt;/code&gt;"><code>match_property</code></h3><p>This function needs to take a property name and value and return a
<code>CSSProperty</code> with the correct field (matching up to the property name
passed in) and assigned to the value passed in.</p>
<p>If we didn&#39;t have metaprogramming or reflection, the implementation
might look like this:</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    if (std.mem.eql(u8, name, &#34;color&#34;)) {
        return CSSProperty{.color = value};
    } else if (std.mem.eql(u8, name, &#34;background&#34;)) {
        return CSSProperty{.background = value};
    }

    return error.UnknownProperty;
}
</code></pre>
<p>And that is not necessarily bad. In fact it may be how a lot of
production code looks over time as product needs evolve. You can keep
the internal field name unrelated to the external field name.</p>
<p>However for the sake of learning, we&#39;ll try to implement the same
thing with Zig metaprogramming.</p>
<p>And specifically, we can take a look at
<a href="https://github.com/ziglang/zig/blob/32cb9462ffa0a9df7a080d67eaf3a5762173f742/lib/std/json/static.zig">lib/std/json/static.zig</a>
to understand the reflection APIs.</p>
<p>Specifically, if we look at line 210-226 of that file, we can see them
iterating over fields of a <code>Union</code>:</p>
<pre><code>        .Union =&gt; |unionInfo| {
            if (comptime std.meta.trait.hasFn(&#34;jsonParse&#34;)(T)) {
                return T.jsonParse(allocator, source, options);
            }

            if (unionInfo.tag_type == null) @compileError(&#34;Unable to parse into untagged union &#39;&#34; ++ @typeName(T) ++ &#34;&#39;&#34;);

            if (.object_begin != try source.next()) return error.UnexpectedToken;

            var result: ?T = null;
            var name_token: ?Token = try source.nextAllocMax(allocator, .alloc_if_needed, options.max_value_len.?);
            const field_name = switch (name_token.?) {
                inline .string, .allocated_string =&gt; |slice| slice,
                else =&gt; return error.UnexpectedToken,
            };

            inline for (unionInfo.fields) |u_field| {
</code></pre>
<p>Then right after that (lines 226-243) we see them conditionally
modifying the result object:</p>
<pre><code>            inline for (unionInfo.fields) |u_field| {
                if (std.mem.eql(u8, u_field.name, field_name)) {
                    // Free the name token now in case we&#39;re using an allocator that optimizes freeing the last allocated object.
                    // (Recursing into parseInternal() might trigger more allocations.)
                    freeAllocated(allocator, name_token.?);
                    name_token = null;

                    if (u_field.type == void) {
                        // void isn&#39;t really a json type, but we can support void payload union tags with {} as a value.
                        if (.object_begin != try source.next()) return error.UnexpectedToken;
                        if (.object_end != try source.next()) return error.UnexpectedToken;
                        result = @unionInit(T, u_field.name, {});
                    } else {
                        // Recurse.
                        result = @unionInit(T, u_field.name, try parseInternal(u_field.type, allocator, source, options));
                    }
                    break;
                }
</code></pre>
<p>We can see that the <code>.Union =&gt; |unionInfo|</code> condition is entered by
switching on <code>@typeInfo(T)</code> (<a href="https://github.com/ziglang/zig/blob/32cb9462ffa0a9df7a080d67eaf3a5762173f742/lib/std/json/static.zig#L149">line
149</a>)
and that <code>T</code> is a type (<a href="https://github.com/ziglang/zig/blob/32cb9462ffa0a9df7a080d67eaf3a5762173f742/lib/std/json/static.zig#L144">line
144</a>).</p>
<p>We don&#39;t have a generic type though. We know we are working with a
<code>CSSProperty</code>. And we know <code>CSSProperty</code> is a union so we don&#39;t need
the <code>switch</code> either.</p>
<p>So let&#39;s apply that to our <code>match_property</code> implementation.</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    const cssPropertyInfo = @typeInfo(CSSProperty);

    for (cssPropertyInfo.Union.fields) |u_field| {
        if (std.mem.eql(u8, u_field.name, name)) {
            return @unionInit(CSSProperty, u_field.name, value);
        }
    }

    return error.UnknownProperty;
}
</code></pre>
<p>And if we try to build that we&#39;ll get an error like this:</p>
<pre><code>main.zig:15:31: error: values of type &#39;[]const builtin.Type.UnionField&#39; must be comptime-known, but index value is runtime-known
    for (cssPropertyInfo.Union.fields) |u_field| {
</code></pre>
<p>Zig&#39;s &#34;reflection&#34; abilities here are comptime only. So we can&#39;t use a
runtime <code>for</code> loop, we must use a comptime <code>inline for</code> loop.</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    const cssPropertyInfo = @typeInfo(CSSProperty);

    inline for (cssPropertyInfo.Union.fields) |u_field| {
        if (std.mem.eql(u8, u_field.name, name)) {
            return @unionInit(CSSProperty, u_field.name, value);
        }
    }

    return error.UnknownProperty;
}
</code></pre>
<p>As far as I understand it, this loop is basically unrolled and the
generated code would look a lot like our hard-coded initial version.</p>
<p>i.e. it would probably look like this:</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    const cssPropertyInfo = @typeInfo(CSSProperty);

    if (std.mem.eql(u8, &#34;background&#34;, name)) {
        return @unionInit(CSSProperty, &#34;background&#34;, value);
    }

    if (std.mem.eql(u8, &#34;color&#34;, name)) {
        return @unionInit(CSSProperty, &#34;color&#34;, value);
    }

    if (std.mem.eql(u8, &#34;unknown&#34;, name)) {
        return @unionInit(CSSProperty, &#34;unknown&#34;, value);
    }

    return error.UnknownProperty;
}
</code></pre>
<p>Again that&#39;s just how I imagine the compiler to generate code from the
Union field reflection and <code>inline for</code> over the fields.</p>
<p>Try compiling that code. I get this:</p>
<pre><code>main.zig:17:58: error: expected type &#39;void&#39;, found &#39;[]const u8&#39;
            return @unionInit(CSSProperty, u_field.name, value);
</code></pre>
<p>Thinking about the generated code makes it especially clear what&#39;s
happening. We have an <code>unknown</code> field in there that has a <code>void</code>
type. You can&#39;t assign to that type.</p>
<p>We know at runtime that the condition where that happens should be
impossible because the user shouldn&#39;t enter <code>unknown</code> as a property
name. (Though now that I write this, I see they actually could. But
let&#39;s pretend they wouldn&#39;t.)</p>
<p>So the problem isn&#39;t a runtime failure but a comptime type-checking
failure.</p>
<p>Thankfully we can work around this with comptime conditionals.</p>
<p>If we wrap our current condition in an additional conditional that is
evaluated at comptime and filters out the <code>unknown</code> pass of the
<code>inline for</code> loop, the compiler shouldn&#39;t generate any code trying to
assign to the <code>unknown</code> field.</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    const cssPropertyInfo = @typeInfo(CSSProperty);

    inline for (cssPropertyInfo.Union.fields) |u_field| {
        if (comptime !std.mem.eql(u8, u_field.name, &#34;unknown&#34;)) {
            if (std.mem.eql(u8, u_field.name, name)) {
                return @unionInit(CSSProperty, u_field.name, value);
            }
        }
    }

    return error.UnknownProperty;
}
</code></pre>
<p>And indeed, if you try to compile it, this works. Since the
conditional is evaluated at compile time, we can imagine the code the
compiler generates is this:</p>
<pre><code>fn match_property(
    name: []const u8,
    value: []const u8,
) !CSSProperty {
    const cssPropertyInfo = @typeInfo(CSSProperty);

    if (std.mem.eql(u8, &#34;background&#34;, name)) {
        return @unionInit(CSSProperty, &#34;background&#34;, value);
    }

    if (std.mem.eql(u8, &#34;color&#34;, name)) {
        return @unionInit(CSSProperty, &#34;color&#34;, value);
    }

    return error.UnknownProperty;
}
</code></pre>
<p>The <code>unknown</code> field has been skipped.</p>
<p>In retrospect, I realize that the <code>unknown</code> field probably isn&#39;t
even needed. We could eliminate it from the <code>CSSProperty</code> union and
get rid of that comptime conditional. However, sometimes there are in
fact private fields you want to skip. And I wanted to show how to
deal with that case.</p>
<p>For the last bit of metaprogramming, let&#39;s talk about displaying
the resulting <code>CSSSheet</code> we&#39;d get after parsing.</p>
<h3 id="&lt;code&gt;sheet.display()&lt;/code&gt;"><code>sheet.display()</code></h3><p>If we didn&#39;t have metaprogramming and wanted to display the sheet,
we&#39;d have to switch on every possible union field.</p>
<p>Like so (I&#39;ve modified the <code>CSSSheet</code> struct definition so it includes this method):</p>
<pre><code>    fn display(sheet: *CSSSheet) void {
        for (sheet.rules) |rule| {
            std.debug.print(&#34;selector: {s}\n&#34;, .{rule.selector});
            for (rule.properties) |property| {
                var string = switch (property) {
                    .unknown =&gt; unreachable,
                    .color =&gt; |color_value| color_value,
                    .background =&gt; |background_value| background_value,
                };
            }
            std.debug.print(&#34;\n&#34;, .{});
        }
    }
</code></pre>
<p>This could get unwieldy as we add fields to the <code>CSSProperty</code> union.</p>
<p>Instead we can use the <code>inline for
(@typeInfo(CSSProperty).Union.fields) |u_field|</code> method to iterate
over all fields, skip the <code>unknown</code> field at comptime, and print out
the field name and value by matching on the current value of the
<code>property</code> enum by using the <code>@tagName</code> builtin.</p>
<pre><code>    fn display(sheet: *CSSSheet) void {
        for (sheet.rules) |rule| {
            std.debug.print(&#34;selector: {s}\n&#34;, .{rule.selector});
            for (rule.properties) |property| {
                inline for (@typeInfo(CSSProperty).Union.fields) |u_field| {
                    if (comptime !std.mem.eql(u8, u_field.name, &#34;unknown&#34;)) {
                        if (std.mem.eql(u8, u_field.name, @tagName(property))) {
                            std.debug.print(&#34;  {s}: {s}\n&#34;, .{
                                @tagName(property),
                                @field(property, u_field.name),
                            });
                        }
                    }
                }
            }
            std.debug.print(&#34;\n&#34;, .{});
        }
    }
</code></pre>
<h3 id="&lt;code&gt;main&lt;/code&gt;"><code>main</code></h3><p>Finally, we pull it all together with a little <code>main</code> function.</p>
<pre><code>pub fn main() !void {
    var arena = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena.deinit();

    const allocator = arena.allocator();

    // Let&#39;s read in a CSS file.
    var args = std.process.args();

    // Skips the program name.
    _ = args.next();

    var file_name: []const u8 = &#34;&#34;;
    if (args.next()) |f| {
        file_name = f;
    }

    const file = try std.fs.cwd().openFile(file_name, .{});
    defer file.close();

    const file_size = try file.getEndPos();
    var css_file = try allocator.alloc(u8, file_size);
    _ = try file.read(css_file);

    var sheet = parse(&amp;arena, css_file) catch return;
    sheet.display();
}
</code></pre>
<p>And try it against some tests.</p>
<pre><code>$ zig build-exe main.zig
$ cat tests/basic.css
div {
    background: white;
}
$ ./main tests/basic.css
selector: div
  background: white
</code></pre>
<p>Nice! Let&#39;s try it against a more complex test.</p>
<pre><code>$ cat tests/multiple-blocks.css
div {
    background: black;
    color: white;
}

a {
    color: blue;
}
$ ./main tests/multiple-blocks.css
selector: div
  background: black
  color: white

selector: a
  color: blue
</code></pre>
<p>Awesome. And against a bad CSS sheet:</p>
<pre><code>$ cat tests/bad-property.css
a {
    big: pink;
}
$ ./main cat tests/bad-property.css
Error at line 2, column 4. Unknown property: &#39;big&#39;.


    big: pink;
    ^ Near here.
</code></pre>
<p>We&#39;ve got it!</p>
<h3 id="addendum:-&lt;code&gt;@field&lt;/code&gt;">Addendum: <code>@field</code></h3><p>The docs were quite clear about using <code>@field(object, fieldName)</code> to
access the value of an <code>object</code> of type <code>@TypeOf(object)</code> at field
<code>fieldName</code>.</p>
<p>And the docs do mention <code>@field()</code> can be used as LHS but that only
really struct me when I was browsing the Zig JSON code like at <a href="https://github.com/ziglang/zig/blob/master/lib/std/json/static.zig#L307">line
307</a>.</p>
<p>I didn&#39;t use that in this little project but I&#39;ve used it elsewhere,
so it I wanted to call this LHS behavior out.</p>
<blockquote><p lang="en" dir="ltr">Wrote a short post on parsing CSS as a way to motivate some basic exploration of metaprogramming in Zig.</p>â€” Phil Eaton (@eatonphil) <a href="https://twitter.com/eatonphil/status/1670868544953647129?ref_src=twsrc%5Etfw">June 19, 2023</a></blockquote> 

	<div>
	  <h4>Feedback</h4>
	  <p><small>As always,
	      please <a href="mailto:phil@eatonphil.com">email</a>
	      or <a href="https://twitter.com/eatonphil">tweet me</a>
	      with questions, corrections, or ideas!</small></p>

	</div>
      </div>
    </div></div>
  </body>
</html>
