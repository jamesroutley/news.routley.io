<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://jmglov.net/blog/2024-01-17-clickr.html">Original</a>
    <h1>Clickr, or a young man&#39;s Flickr clonejure</h1>
    
    <div id="readability-page-1" class="page"><div>

      
<p><img src="https://jmglov.net/blog/assets/2024-01-17-phoenix.jpg" alt="A space station" title="The Phoenix rises!" width="800px"/></p><p>Way back when, before I got my first Macbook (the white 2006 model, so cool!) and thus had no iPhoto to keep all of my photos for me, I used some open source photo album thingy on Linux (it was so long ago that I don&#39;t even remember what it was called) on a server running under my desk with a fixed IP. Using this amazing setup, I could create photo albums and share them with family who wanted to keep up with my thrilling life. When we moved to Tokyo in 2005, I got rid of the Linux server (and the desk it was under and the apartment the desk was in) and brought only a laptop with me. When we first arrived, we didn&#39;t even have internet, so I borrowed wifi from someone in the next building who had helpfully eschewed a password, and I thought it would be a bit cheeky to locate that person and ask them to purchase a fixed IP so that I could run a webserver so I could share photo albums with my family.</p><p>Luckily for me, two people named Stewart Butterfield and Caterina Fake felt my pain and started a company called Ludicorp and tried to create a web-based massively multiplayer online game called Game Neverending, which they failed to do but made some pretty cool tools for assets which they then decided could be turned into an online photo sharing service which they named Flickr.</p><p>Which is a very long-winded way of sayingâ€”and unless you&#39;re very new to this blog, you will know that I am a very long-winded personâ€”that I signed up for Flickr for my photo-sharing needs.</p><p>A few years later, I bought the aforementioned Macbook, and started using iPhoto, which was much nicer than copying photos from a memory card onto my hard drive and then uploading them to Flickr, so I got kinda lazy about creating albums on Flickr, until my son was born and my family started demanding to see pictures of him.</p><p>Flash forward to many years later, and now we all have smartphones and stuff (even my mom!), so we just take pictures which automatically go to iCloud and iPhoto and iDon&#39;tKnowWhereElse and then share them with each other in Signal and Telegram and WhatsApp and 95 other smartphone messaging apps like normal people, so we don&#39;t really use Flickr any more. In fact, I only remember we have Flickr when they email me once a year to remind me they&#39;re about to charge my credit card another $100 or whatever.</p><p>My reluctance to pay for something that I don&#39;t use is exceeded only by my reluctance to spend time on manually migrating off that thing, so I didn&#39;t do anything about this until a couple of years ago, when I just so happened to get the annual renewal email right before my winter vacation started, so I was like &#34;Oh, now I&#39;ll have some free time and I&#39;ll probably be bored so I should probably see if Flickr has an API that I could use to download my photos and put them onto S3 like the good lord intended.&#34;</p><p>Flickr did in fact have an API, and better yet, they had a <a href="https://github.com/boncey/Flickr4Java">Java
client</a>, which I wrapped up in some Clojure and named <a href="https://github.com/jmglov/clickr">clickr</a> because it&#39;s like Flickr in Clojure ha ha ha I&#39;m so clever. I got the listing of albums and downloading of photos and uploading of them to S3 working pretty easily, but for some reason didn&#39;t actually back up the albums to S3 except for the first one, probably because I evaluated some code in the REPL to start the back up and then switched tabs to read a blog or something and then forgot what I was doing and closed my laptop or something similarly absentminded.</p><p>Flash forward to a couple of weeks ago, when I once again got the famous email from Flickr and remembered that I really should get around to backing up those albums so I can stop paying Flickr and pay AWS a few more cents a year to store them for me. Since I now hate Clojure and only love <a href="https://babashka.org/">Babashka</a>, I decided that instead of going back to the Clojure wrapper around the Java library, I&#39;d whip up a quick HTTP client in Babashka instead and then just use <a href="https://github.com/grzm/awyeah-api">awyeah-api</a> for the S3-ing. I got a few hours into struggling with signing requests to get an OAuth access token which I could then get the user&#39;s (me) authorization to exchange for an access tokenâ€”you know, <a href="https://www.flickr.com/services/api/auth.oauth.html">as you do</a>â€”when I decided that my life was way to short for taking a URL such as</p><pre><code>https://www.flickr.com/services/oauth/request_token
?oauth_nonce=89601180
&amp;oauth_timestamp=1305583298
&amp;oauth_consumer_key=653e7a6ecc1d528c516cc8f92cf98611
&amp;oauth_signature_method=HMAC-SHA1
&amp;oauth_version=1.0
&amp;oauth_callback=http%3A%2F%2Fwww.example.com
</code></pre><p>and creating a base string such as</p><pre><code>GET&amp;https%3A%2F%2Fwww.flickr.com%2Fservices%2Foauth%2Frequest_token&amp;oauth_callback%3Dhttp%253A%252F%252Fwww.example.com%26oauth_consumer_key%3D653e7a6ecc1d528c516cc8f92cf98611%26oauth_nonce%3D95613465%26oauth_signature_method%3DHMAC-SHA1%26oauth_timestamp%3D1305586162%26oauth_version%3D1.0
</code></pre><p>and then generating the following signature</p><pre><code>7w18YS2bONDPL%2FzgyzP5XTr5af4%3D
</code></pre><p>and then wondering why in the hell Flickr&#39;s API kept giving me a signature incorrect error when the damn signature was obviously correct because you know...</p><p><img src="https://jmglov.net/blog/assets/2024-01-17-flickr-oauth-flow.jpg" alt="A diagram showing the three step OAuth flow for Flickr" title="What in the actual what?" width="800px"/></p><p>I rapidly came to the conclusion that I should retreat to the safe embrace of JVM Clojure and just let the Java client do all the nasty work of authenticating so I could do the fun work of Clojuring.</p><h2 id="onwards_and_upwards!">Onwards and upwards!</h2><p>I cloned my trusty <a href="https://github.com/jmglov/clickr">clickr</a> repo, then screamed as I saw a <code>project.clj</code> and realised that I didn&#39;t even have <a href="https://leiningen.org/">Leiningen</a> installed because I roll with <a href="https://clojure.org/guides/deps_and_cli">tools.deps</a> now and I can&#39;t remember any lein commands and OMG it&#39;s nearly 2024 (this was a few weeks ago, remember?) and I&#39;d better generate a <code>deps.edn</code> like a normal person:</p><pre><code>{:paths [&#34;src&#34; &#34;dev&#34;]
 :deps {com.flickr4java/flickr4java {:mvn/version &#34;3.0.1&#34;}}}
</code></pre><p>Having done this, I could open <code>src/clickr/flickr.clj</code> and try and remember how to use some code I wrote a <del>million</del> couple years ago and didn&#39;t document in any way, shape, or form because I&#39;m a bad boy and life&#39;s too short for documentation!</p><p>According to Flickr4Java:</p><blockquote><p> To use the API just construct an instance of the class  com.flickr4java.flickr.test.Flickr and request the interfaces which you need  to work with. </p></blockquote><pre><code>String apiKey = &#34;YOUR_API_KEY&#34;;
String sharedSecret = &#34;YOUR_SHARED_SECRET&#34;;
Flickr f = new Flickr(apiKey, sharedSecret, new REST());
</code></pre><p>OK, so we can start our namespace out by importing the Flickr client:</p><pre><code>(ns clickr.flickr
  (:import (com.flickr4java.flickr Flickr
                                   REST)))
</code></pre><p>And then C-c M-j (<code>cider-jack-in-clj</code>) to start a REPL, and then C-c C-k (<code>cider-load-buffer</code>) to evaluate the buffer, then it seems like I need an API key and the corresponding secret, which I can grab from https://www.flickr.com/services/apps/by/jmglov. Lemme just drop that in a map for safe-keeping and do a little C-v f c e (<code>cider-pprint-eval-last-sexp-to-comment</code>, obv):</p><pre><code>(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;})
  ;; =&gt; #&#39;clickr.flickr/config

  )
</code></pre><p>OK, so creating a client should be as simple as this:</p><pre><code>  (def flickr (Flickr. (:api-key config) (:secret config) (REST.)))
  ;; =&gt; #&#39;clickr.flickr/flickr
</code></pre><p>And with this, it should be easy to figure out how to get a list of albums, right? Right?</p><h2 id="what_the_flickr?">What the Flickr?</h2><p>Popping over to <a href="https://www.flickr.com/services/api/">Flickr&#39;s API reference</a>, I&#39;ll just do a quick search for album and... 0 occurrences? Wait what now?</p><p>OK, time to scan the page for something that looks album-ish. ðŸ™„</p><p>Ah, <a href="https://www.flickr.com/services/api/flickr.photosets.getList.html">photosets.getList</a> looks promising. <a href="https://github.com/search?q=repo%3Aboncey%2FFlickr4Java%20photoset&amp;type=code">Searching the Flickr4Java repo for
photoset</a> turns up a <a href="https://github.com/boncey/Flickr4Java/blob/e6b8b51f89c87260039485427ed0b7dbb1354eed/src/examples/java/Backup.java">src/examples/java/Backup.java</a>, which looks quite helpful:</p><pre><code>public class Backup {
    private final String nsid;
    private final Flickr flickr;
    private AuthStore authStore;

    public Backup(String apiKey, String nsid, String sharedSecret, File authsDir) throws FlickrException {
        flickr = new Flickr(apiKey, sharedSecret, new REST());
        this.nsid = nsid;

        if (authsDir != null) {
            this.authStore = new FileAuthStore(authsDir);
        }
    }

    // ...

    public static void main(String[] args) throws Exception {
        if (args.length &lt; 4) {
            System.out.println(&#34;Usage: java &#34; + Backup.class.getName() + &#34; api_key nsid shared_secret output_dir&#34;);
            System.exit(1);
        }
        Backup bf = new Backup(args[0], args[1], args[2], new File(System.getProperty(&#34;user.home&#34;) + File.separatorChar + &#34;.flickrAuth&#34;));
        bf.doBackup(new File(args[3]));
    }
}
</code></pre><p>If we look at the <code>doBackup()</code> method, it looks like there&#39;s some authorisation we need to do above and beyond the API key and secret, which aligns with all the Flickr API docs said about &#34;every request must be signed OAuth blah blah blah&#34;:</p><pre><code>RequestContext rc = RequestContext.getRequestContext();

if (this.authStore != null) {
    Auth auth = this.authStore.retrieve(this.nsid);
    if (auth == null) {
        this.authorize();
    } else {
        rc.setAuth(auth);
    }
}
</code></pre><p>Assuming we have no <code>Auth</code> object from the start, let&#39;s have a look at what the <code>authorize()</code> method does:</p><pre><code>private void authorize() throws IOException, FlickrException {
    AuthInterface authInterface = flickr.getAuthInterface();
    OAuth1RequestToken requestToken = authInterface.getRequestToken();

    String url = authInterface.getAuthorizationUrl(requestToken, Permission.READ);
    System.out.println(&#34;Follow this URL to authorise yourself on Flickr&#34;);
    System.out.println(url);
    System.out.println(&#34;Paste in the token it gives you:&#34;);
    System.out.print(&#34;&gt;&gt;&#34;);

    String tokenKey = new Scanner(System.in).nextLine();

    OAuth1Token accessToken = authInterface.getAccessToken(requestToken, tokenKey);

    Auth auth = authInterface.checkToken(accessToken);
    RequestContext.getRequestContext().setAuth(auth);
    this.authStore.store(auth);
    System.out.println(&#34;Thanks.  You probably will not have to do this every time.  Now starting backup.&#34;);
}
</code></pre><p>OK, translating this to Clojure is pretty straight-forward. I&#39;ll start with my usual approach when dealing with API clients of creating a &#34;context&#34; which contains my config, various clients, and any other data that I might need to pass around. Let&#39;s create an <code>init-client</code> function that takes my config as an argument and returns such a context:</p><pre><code>(defn init-client [{:keys [api-key secret] :as ctx}]
  (assoc ctx :flickr {:client (Flickr. api-key secret (REST.))}))
</code></pre><p>We can call that and get a context back containing the client:</p><pre><code>(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;})
  ;; =&gt; #&#39;clickr.flickr/config

  (init-client config)
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :flickr
  ;;     {:client
  ;;      #object[com.flickr4java.flickr.Flickr 0x12246439 &#34;com.flickr4java.flickr.Flickr@12246439&#34;]}}

  )
</code></pre><p>Now I have a client, but I need to perform the authorisation dance from the Java code. In the constructor of the Java class, they create a <code>FileAuthStore</code> in the <code>authsDir</code>, which is actually the directory called <code>output_dir</code> in the <code>main()</code> function. ðŸ™„</p><p>Let&#39;s be a good citizen by creating this auth store in the user&#39;s home directory. In order to do this, let&#39;s add the awesome <a href="https://github.com/babashka/fs">babashka.fs</a> library to our deps:</p><pre><code>{:paths [&#34;src&#34; &#34;dev&#34;]
 :deps {babashka/fs {:mvn/version &#34;0.4.19&#34;}
        com.flickr4java/flickr4java {:mvn/version &#34;3.0.1&#34;}}}
</code></pre><p>babashka.fs contains a bunch of really useful utility functions that work in both Babashka and JVM Clojure!</p><p>Tragically, after adding a new dep, I need to restart my REPL. It is possible to hotload dependencies into a running REPL process, which should have <a href="https://clojure.org/news/2023/04/14/clojure-1-12-alpha2">gotten
easier in Clojure
1.12</a>, and of course borkdude has a <a href="https://github.com/borkdude/deps.add-lib">deps.add-lib</a> project which should make it even easier, but I haven&#39;t used this stuff yet, so I&#39;ll just do the primitive thing and restart (swearing under my breath), of course.</p><p>Now that we have babashka.fs, creating a <code>FileAuthStore</code> in the <code>~/.flickrAuth</code> directory is as simple as this:</p><pre><code>(ns clickr.flickr
  (:require [babashka.fs :as fs])
  (:import (com.flickr4java.flickr Flickr
                                   REST)
           (com.flickr4java.flickr.util FileAuthStore)))

(defn make-auth-store []
  (FileAuthStore. (fs/file (fs/home) &#34;.flickrAuth&#34;)))
</code></pre><p>Let&#39;s create the auth store in <code>init-client</code> and add it to the context along with the Flickr client itself:</p><pre><code>(defn init-client [{:keys [api-key secret] :as ctx}]
  (let [client (Flickr. api-key secret (REST.))
        auth-store (make-auth-store)]
    (assoc ctx :flickr {:client client, :auth-store auth-store})))
</code></pre><p>Now that we have a way to create an auth store, let&#39;s write an <code>authorise</code> function to exchange a request token for an access token:</p><pre><code>(ns clickr.flickr
  (:require [babashka.fs :as fs])
  (:import (com.flickr4java.flickr Flickr
                                   REST)
           (com.flickr4java.flickr.auth Permission)
           (com.flickr4java.flickr.util FileAuthStore)))

(defn authorise [{:keys [flickr] :as ctx}]
  (let [{:keys [client]} flickr
        auth-interface (.getAuthInterface client)
        req-token (.getRequestToken auth-interface)
        url (.getAuthorizationUrl auth-interface req-token Permission/READ)]
    (update ctx :flickr assoc :url url, :req-token req-token)))
</code></pre><p>Let&#39;s try it out:</p><pre><code>(comment

  (def ctx (init-client config))
  ;; =&gt; #&#39;clickr.flickr/ctx

  (authorise ctx)
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :flickr
  ;;     {:client
  ;;      #object[com.flickr4java.flickr.Flickr 0x12246439 &#34;com.flickr4java.flickr.Flickr@12246439&#34;]
  ;;      :auth-store
  ;;      #object[com.flickr4java.flickr.util.FileAuthStore 0x6b1f411f &#34;com.flickr4java.flickr.util.FileAuthStore@6b1f411f&#34;]
  ;;      :url
  ;;      &#34;https://www.flickr.com/services/oauth/authorize?oauth_token=72157720906512072-49cebfb020e7fb06&amp;perms=read&#34;
  ;;      :req-token
  ;;      #object[com.github.scribejava.core.model.OAuth1RequestToken 0x2617e72c &#34;com.github.scribejava.core.model.OAuth1RequestToken@43e76617&#34;]}}

  )
</code></pre><p>If I visit that URL in a web browser where we&#39;re signed into Flickr, I&#39;m prompted to authorise the app corresponding to the API key I previously created:</p><p><img src="https://jmglov.net/blog/assets/2024-01-17-flickr-prompt.png" alt="A prompt to authorize an app to my Flickr account" title="What could possibly go wrong?" width="800px"/></p><p>If I click &#34;OK, I&#39;ll authorize it&#34;, I get a code that I should &#34;type into the application&#34;. Let&#39;s set that up by modifying the <code>authorise</code> function to accept a code, which it will use to exchange the request token for an access token, then store that access token in the auth store that we previously created.</p><pre><code>(ns clickr.flickr
  (:require [babashka.fs :as fs])
  (:import (com.flickr4java.flickr Flickr
                                   RequestContext
                                   REST)
           (com.flickr4java.flickr.auth Permission)
           (com.flickr4java.flickr.util FileAuthStore)))

(defn authorise [{:keys [flickr] :as ctx}]
  (let [{:keys [client auth-store req-token token-key]} flickr
        auth-interface (.getAuthInterface client)]
    (if (and req-token token-key)
      (let [access-token (.getAccessToken auth-interface req-token token-key)
            auth (.checkToken auth-interface access-token)]
        (.setAuth (RequestContext/getRequestContext) auth)
        (.store auth-store auth)
        (update ctx :flickr dissoc :req-token :token-key :url))
      (let [req-token (.getRequestToken auth-interface)
            url (.getAuthorizationUrl auth-interface req-token Permission/READ)]
        (update ctx :flickr assoc :url url, :req-token req-token)))))
</code></pre><p>The idea here is that if you pass a request token and token code in the Flickr context, you want to exchange a request token for an access token, and if you don&#39;t, you want to obtain a request token and the URL to authorise it.</p><p>OK, let&#39;s give it a whirl:</p><pre><code>(comment

  (def ctx (authorise ctx))
  ;; =&gt; #&#39;clickr.flickr/ctx

  (get-in ctx [:flickr :url])
  ;; =&gt; &#34;https://www.flickr.com/services/oauth/authorize?oauth_token=72157720906550331-8cf736dfbfe34398&amp;perms=read&#34;

  (authorise (assoc-in ctx [:flickr :token-key] &#34;123-456-789&#34;))
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :flickr
  ;;     {:client
  ;;      #object[com.flickr4java.flickr.Flickr 0x67f9df60 &#34;com.flickr4java.flickr.Flickr@67f9df60&#34;],
  ;;      :auth-store
  ;;      #object[com.flickr4java.flickr.util.FileAuthStore 0x237de92 &#34;com.flickr4java.flickr.util.FileAuthStore@237de92&#34;]}}

  )
</code></pre><p>In theory, we should now have a Flickr4Java request context with an access token, and that access token stored in our auth store. Let&#39;s check out what&#39;s in the auth store:</p><pre><code>(comment

  (.retrieveAll (get-in ctx [:flickr :auth-store]))
  ;; =&gt; [#object[com.flickr4java.flickr.auth.Auth 0x7737c4f2 &#34;com.flickr4java.flickr.auth.Auth@7737c4f2&#34;]]

  )
</code></pre><p>Ooh, exciting!</p><p>If we cast our mind back to that Java code:</p><pre><code>Auth auth = this.authStore.retrieve(this.nsid);
if (auth == null) {
    this.authorize();
} else {
    rc.setAuth(auth);
}
</code></pre><p>we see that the <code>authorize()</code> method is only called if we don&#39;t have an access token (the <code>Auth</code> object). If we have one, we just stuff it in the request context and move on with our life. Let&#39;s make one final change to our <code>authorise</code> function to do the same thing:</p><pre><code>(defn authorise [{:keys [flickr] :as ctx}]
  (let [{:keys [client auth-store req-token token-key]} flickr
        auth-interface (.getAuthInterface client)
        auth (-&gt; auth-store .retrieveAll first)]
    (cond
      ;; We have a valid access token from the auth store
      auth
      (do
        (.setAuth (RequestContext/getRequestContext) auth)
        (update ctx :flickr assoc :auth auth))

      ;; We have a request token and a token key, so exchange the request
      ;; token for an access token
      (and req-token token-key)
      (let [access-token (.getAccessToken auth-interface req-token token-key)
            auth (.checkToken auth-interface access-token)]
        (.setAuth (RequestContext/getRequestContext) auth)
        (.store auth-store auth)
        (update ctx :flickr dissoc :req-token :token-key :url))

      ;; We don&#39;t have any tokens, so grab a request token and the URL to
      ;; authorise it
      :default
      (let [req-token (.getRequestToken auth-interface)
            url (.getAuthorizationUrl auth-interface req-token Permission/READ)]
        (update ctx :flickr assoc :url url, :req-token req-token)))))
</code></pre><p>We can see if it works by creating a brand new client and calling <code>authorise</code> on it:</p><pre><code>(comment

  (-&gt; (init-client config) authorise)
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :flickr
  ;;     {:client
  ;;      #object[com.flickr4java.flickr.Flickr 0x15c343a9 &#34;com.flickr4java.flickr.Flickr@15c343a9&#34;],
  ;;      :auth-store
  ;;      #object[com.flickr4java.flickr.util.FileAuthStore 0x6df7251f &#34;com.flickr4java.flickr.util.FileAuthStore@6df7251f&#34;]
  ;;      :auth
  ;;      #object[com.flickr4java.flickr.auth.Auth 0x4d1d0eb0 &#34;com.flickr4java.flickr.auth.Auth@4d1d0eb0&#34;]}}

  )
</code></pre><p>That calls for a celebration!</p><p><img src="https://jmglov.net/blog/assets/2023-11-11-victory.jpg" alt="A woman on a beach at sunrise with her head thrown back, saying " victory""="" title="Victory!" width="800px]"/></p><p>Given that you only need to go through the rigmarole of exchanging a request token for an auth token once, I feel like it&#39;s reasonable to have <code>init-client</code> go ahead and authorise the client. This will work just fine for the rigmarole case as well, since you can just call <code>authorise</code> on the client yourself if it has a <code>:url</code> key set, so there&#39;s no harm in doing this.</p><pre><code>(defn init-client [{:keys [api-key secret] :as ctx}]
  (let [client (Flickr. api-key secret (REST.))
        auth-store (make-auth-store)]
    (-&gt; ctx
        (assoc :flickr {:client client, :auth-store auth-store})
        authorise)))
</code></pre><h2 id="ok,_so_what_about_those_albums_again?">OK, so what about those albums again?</h2><p>Before we got sidetracked by all of this annoying security stuff, the actual goal was to get a list of my photo albums, which we learned are apparently called &#34;photosets&#34; in the Flickr API. Let&#39;s have a look at how the example Java code deals with them:</p><pre><code>PhotosetsInterface pi = flickr.getPhotosetsInterface();
Iterator sets = pi.getList(this.nsid).getPhotosets().iterator();
</code></pre><p>The <code>nsid</code> thingy turns out to just be my Flickr user ID, which I can grab from the <code>Auth</code> object. Let&#39;s grab an album and see what it&#39;s all about!</p><pre><code>(comment

  (def album
    (let [ps-interface (.getPhotosetsInterface (get-in ctx [:flickr :client]))
          user-id (.. (get-in ctx [:flickr :auth]) getUser getId)]
      (-&gt; ps-interface (.getList user-id) .getPhotosets first)))
  ;; =&gt; #&#39;clickr.flickr/album

  album
  ;; =&gt; #object[com.flickr4java.flickr.photosets.Photoset 0x726a87d4 &#34;com.flickr4java.flickr.photosets.Photoset@726a87d4&#34;]

  )
</code></pre><p>Cool, I guess. Looking at the Flickr API reference for <a href="https://www.flickr.com/services/api/flickr.photosets.getList.html">photosets.getList</a>, we can see an example XML response:</p><pre><code>&lt;photosets page=&#34;1&#34; pages=&#34;1&#34; perpage=&#34;30&#34; total=&#34;2&#34; cancreate=&#34;1&#34;&gt;
  &lt;photoset id=&#34;72157626216528324&#34; primary=&#34;5504567858&#34; secret=&#34;017804c585&#34; server=&#34;5174&#34; farm=&#34;6&#34; photos=&#34;22&#34; videos=&#34;0&#34; count_views=&#34;137&#34; count_comments=&#34;0&#34; can_comment=&#34;1&#34; date_create=&#34;1299514498&#34; date_update=&#34;1300335009&#34;&gt;
    &lt;title&gt;Avis Blanche&lt;/title&gt;
    &lt;description&gt;My Grandma&#39;s Recipe File.&lt;/description&gt;
  &lt;/photoset&gt;
  &lt;photoset id=&#34;72157624618609504&#34; primary=&#34;4847770787&#34; secret=&#34;6abd09a292&#34; server=&#34;4153&#34; farm=&#34;5&#34; photos=&#34;43&#34; videos=&#34;12&#34; count_views=&#34;523&#34; count_comments=&#34;1&#34; can_comment=&#34;1&#34; date_create=&#34;1280530593&#34; date_update=&#34;1308091378&#34;&gt;
    &lt;title&gt;Mah Kittehs&lt;/title&gt;
    &lt;description&gt;Sixty and Niner. Born on the 3rd of May, 2010, or thereabouts. Came to my place on Thursday, July 29, 2010.&lt;/description&gt;
  &lt;/photoset&gt;
&lt;/photosets&gt;
</code></pre><p>Let&#39;s make some assumptions about what getters the <code>Photoset</code> class has and see if we can find out anything interesting about our album:</p><pre><code>(comment

  (.getId album)
  ;; =&gt; &#34;72177720314024335&#34;

  (.getTitle album)
  ;; =&gt; &#34;clickr demo&#34;

  (.getDescription album)
  ;; =&gt; &#34;Photo album demo for my clickr blog post&#34;

  )
</code></pre><p>Nice! Given this, let&#39;s write a function that turns a <code>Photoset</code> into a plain old map (or POMâ€”that&#39;s what that means, right?):</p><pre><code>(defn -&gt;album [photoset]
  {:id (.getId photoset)
   :title (.getTitle photoset)
   :description (.getDescription photoset)
   :object photoset})

(comment

  (-&gt;album album)
  ;; =&gt; {:id &#34;72177720314024335&#34;,
  ;;     :title &#34;clickr demo&#34;,
  ;;     :description &#34;Photo album demo for my clickr blog post&#34;,
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photosets.Photoset 0x726a87d4 &#34;com.flickr4java.flickr.photosets.Photoset@726a87d4&#34;]}

  )
</code></pre><p>And now that we have this, why not a function that grabs all of my albums?</p><pre><code>(defn get-albums [{:keys [flickr] :as ctx}]
  (let [user-id (.. (:auth flickr) getUser getId)]
    (-&gt;&gt; (-&gt; (:client flickr)
             (.getPhotosetsInterface)
             (.getList user-id)
             (.getPhotosets))
         (map -&gt;album))))

(comment

  (-&gt;&gt; (get-albums ctx)
       count)
  ;; =&gt; 165

  (-&gt;&gt; (get-albums ctx)
       (take 2))
  ;; =&gt; ({:id &#34;72177720314024335&#34;,
  ;;      :title &#34;clickr demo&#34;,
  ;;      :description &#34;Photo album demo for my clickr blog post&#34;,
  ;;      :object
  ;;      #object[com.flickr4java.flickr.photosets.Photoset 0x9d8b7ae &#34;com.flickr4java.flickr.photosets.Photoset@9d8b7ae&#34;]}
  ;;     {:id &#34;72157706528674711&#34;,
  ;;      :title &#34;Kai&#39;s 8th Birthday&#34;,
  ;;      :description
  ;;      &#34;April 2015. Kaiche&#39;s first year at SIS. Treating his classmates to muffins in class. Sushi and cake with mommy and daddy. &#34;,
  ;;      :object
  ;;      #object[com.flickr4java.flickr.photosets.Photoset 0x1bb741fe &#34;com.flickr4java.flickr.photosets.Photoset@1bb741fe&#34;]})

  )
</code></pre><p>Alright, it looks like I have quite a few albums to deal with here. But an album should contain some photos, right? Let&#39;s see if we can figure out how to grab them.</p><p>In Java, that apparently looks like this:</p><pre><code>Photoset set = (Photoset) sets.next();
PhotoList photos = pi.getPhotos(set.getId(), 500, 1);
</code></pre><p>Let&#39;s try that in Clojure:</p><pre><code>(comment

  (def album (-&gt; (get-albums ctx) first))
  ;; =&gt; #&#39;clickr.flickr/album

  (def ps-interface (.getPhotosetsInterface (get-in ctx [:flickr :client])))
  ;; =&gt; #&#39;clickr.flickr/ps-interface

  (def photos (.getPhotos ps-interface (:id album) 500 1))
  ;; =&gt; #&#39;clickr.flickr/photos

  (count photos)
  ;; =&gt; 8

  (first photos)
  ;; =&gt; #object[com.flickr4java.flickr.photos.Photo 0xd254585 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;]
  )
</code></pre><p>According to the Flickr API documentation for <a href="https://www.flickr.com/services/api/flickr.photosets.getPhotos.html">photosets.getPhotos</a>, the <code>500</code> and the <code>1</code> are the number of photos to return per page and the page number, both of which are the default values. Seems fine.</p><p>The docs are a little sparse on what&#39;s in a photo, though:</p><pre><code>&lt;photoset id=&#34;4&#34; primary=&#34;2483&#34; page=&#34;1&#34; perpage=&#34;500&#34; pages=&#34;1&#34; total=&#34;2&#34;&gt;
  &lt;photo id=&#34;2484&#34; secret=&#34;123456&#34; server=&#34;1&#34; title=&#34;my photo&#34; isprimary=&#34;0&#34; /&gt;
  &lt;photo id=&#34;2483&#34; secret=&#34;123456&#34; server=&#34;1&#34; title=&#34;flickr rocks&#34; isprimary=&#34;1&#34; /&gt;
&lt;/photoset&gt;
</code></pre><p>Let&#39;s ask the JVM what getters the photo class has:</p><pre><code>  (def photo (first photos))
  ;; =&gt; #&#39;clickr.flickr/photo

  (-&gt;&gt; photo class .getDeclaredMethods
       (map #(.getName %))
       (filter #(str/starts-with? % &#34;get&#34;))
       sort)
  ;; =&gt; (&#34;getBaseImageUrl&#34;
  ;;     &#34;getComments&#34;
  ;;     &#34;getCountry&#34;
  ;;     &#34;getCounty&#34;
  ;;     &#34;getDateAdded&#34;
  ;;     &#34;getDatePosted&#34;
  ;;     &#34;getDateTaken&#34;
  ;;     &#34;getDescription&#34;
  ;;     &#34;getEditability&#34;
  ;;     &#34;getFarm&#34;
  ;;     &#34;getGeoData&#34;
  ;;     &#34;getHdMp4&#34;
  ;;     &#34;getHdMp4Url&#34;
  ;;     &#34;getIconFarm&#34;
  ;;     &#34;getIconServer&#34;
  ;;     &#34;getId&#34;
  ;;     &#34;getImage&#34;
  ;;     &#34;getImageAsStream&#34;
  ;;     &#34;getLarge1600Size&#34;
  ;;     &#34;getLarge1600Url&#34;
  ;;     &#34;getLarge2048Size&#34;
  ;;     &#34;getLarge2048Url&#34;
  ;;     &#34;getLargeAsStream&#34;
  ;;     &#34;getLargeImage&#34;
  ;;     &#34;getLargeSize&#34;
  ;;     &#34;getLargeUrl&#34;
  ;;     &#34;getLastUpdate&#34;
  ;;     &#34;getLicense&#34;
  ;;     &#34;getLocality&#34;
  ;;     &#34;getMedia&#34;
  ;;     &#34;getMediaStatus&#34;
  ;;     &#34;getMedium640Size&#34;
  ;;     &#34;getMedium640Url&#34;
  ;;     &#34;getMedium800Size&#34;
  ;;     &#34;getMedium800Url&#34;
  ;;     &#34;getMediumAsStream&#34;
  ;;     &#34;getMediumImage&#34;
  ;;     &#34;getMediumSize&#34;
  ;;     &#34;getMediumUrl&#34;
  ;;     &#34;getMobileMp4&#34;
  ;;     &#34;getMobileMp4Url&#34;
  ;;     &#34;getNotes&#34;
  ;;     &#34;getOriginalAsStream&#34;
  ;;     &#34;getOriginalBaseImageUrl&#34;
  ;;     &#34;getOriginalFormat&#34;
  ;;     &#34;getOriginalHeight&#34;
  ;;     &#34;getOriginalImage&#34;
  ;;     &#34;getOriginalImage&#34;
  ;;     &#34;getOriginalImageAsStream&#34;
  ;;     &#34;getOriginalSecret&#34;
  ;;     &#34;getOriginalSize&#34;
  ;;     &#34;getOriginalUrl&#34;
  ;;     &#34;getOriginalWidth&#34;
  ;;     &#34;getOwner&#34;
  ;;     &#34;getPathAlias&#34;
  ;;     &#34;getPermissions&#34;
  ;;     &#34;getPhotoUrl&#34;
  ;;     &#34;getPlaceId&#34;
  ;;     &#34;getPublicEditability&#34;
  ;;     &#34;getRegion&#34;
  ;;     &#34;getRotation&#34;
  ;;     &#34;getSecret&#34;
  ;;     &#34;getServer&#34;
  ;;     &#34;getSiteMP4Size&#34;
  ;;     &#34;getSiteMP4Url&#34;
  ;;     &#34;getSizes&#34;
  ;;     &#34;getSmall320Size&#34;
  ;;     &#34;getSmall320Url&#34;
  ;;     &#34;getSmallAsInputStream&#34;
  ;;     &#34;getSmallImage&#34;
  ;;     &#34;getSmallSize&#34;
  ;;     &#34;getSmallSquareAsInputStream&#34;
  ;;     &#34;getSmallSquareImage&#34;
  ;;     &#34;getSmallSquareUrl&#34;
  ;;     &#34;getSmallUrl&#34;
  ;;     &#34;getSquareLargeSize&#34;
  ;;     &#34;getSquareLargeUrl&#34;
  ;;     &#34;getSquareSize&#34;
  ;;     &#34;getStats&#34;
  ;;     &#34;getTags&#34;
  ;;     &#34;getTakenGranularity&#34;
  ;;     &#34;getThumbnailAsInputStream&#34;
  ;;     &#34;getThumbnailImage&#34;
  ;;     &#34;getThumbnailSize&#34;
  ;;     &#34;getThumbnailUrl&#34;
  ;;     &#34;getTitle&#34;
  ;;     &#34;getUrl&#34;
  ;;     &#34;getUrls&#34;
  ;;     &#34;getUsage&#34;
  ;;     &#34;getVideoOriginalSize&#34;
  ;;     &#34;getVideoOriginalUrl&#34;
  ;;     &#34;getVideoPlayerSize&#34;
  ;;     &#34;getVideoPlayerUrl&#34;
  ;;     &#34;getViews&#34;)
</code></pre><p>Let&#39;s write a <code>-&gt;photo</code> function to POM-ify this suckah!</p><pre><code>(defn -&gt;photo [photo]
  {:id (.getId photo)
   :title (.getTitle photo)
   :description (.getDescription photo)
   :date-taken (.getDateTaken photo)
   :width (.getOriginalWidth photo)
   :height (.getOriginalHeight photo)
   :geo-data (.getGeoData photo)
   :rotation (.getRotation photo)
   :object photo})

(comment

  (-&gt;photo photo)
  ;; =&gt; {:description nil,
  ;;     :date-taken nil,
  ;;     :geo-data nil,
  ;;     :rotation -1,
  ;;     :width 0,
  ;;     :title &#34;sean-hargreaves-phoenix-new-5-final-a&#34;,
  ;;     :id &#34;53460147147&#34;,
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photos.Photo 0xd254585 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;],
  ;;     :height 0}

  )
</code></pre><p>And now that we have this, let&#39;s add the photos to the the album map:</p><pre><code>(defn -&gt;album [{:keys [flickr] :as ctx} photoset]
  (let [ps-interface (.getPhotosetsInterface (:client flickr))]
    {:id (.getId photoset)
     :title (.getTitle photoset)
     :description (.getDescription photoset)
     :photos (-&gt;&gt; (.getPhotos ps-interface (:id album) 500 1)
                  (map (partial -&gt;photo ctx)))
     :object photoset}))
</code></pre><p>Note that in order to list the photos in an album, we need the <code>PhotosetInterface</code>, which we get from the Flickr client in the context, so we need to add the context as an argument to <code>-&gt;album</code>. In fact, let&#39;s make it a convention that <code>ctx</code> is always the first argument to functions in this namespace, and add it to <code>-&gt;photo</code> as well:</p><pre><code>(defn -&gt;photo [_ctx photo]
  {:id (.getId photo)
   :title (.getTitle photo)
   :description (.getDescription photo)
   :date-taken (.getDateTaken photo)
   :width (.getOriginalWidth photo)
   :height (.getOriginalHeight photo)
   :geo-data (.getGeoData photo)
   :rotation (.getRotation photo)
   :object photo})
</code></pre><p>We don&#39;t actually need it in this function, so we&#39;ll prepend an underscore to the arg name so CIDER or LSP or clj-kondo or whatever tooling you&#39;re using won&#39;t yell at us that it&#39;s unused. This is a convention that I first discovered in Erlang, and I think it&#39;s a cool way to say that &#34;this is the context, which we don&#39;t need here (yet)&#34;.</p><p>Finally, we need to pass the context along in <code>get-albums</code> as well:</p><pre><code>(defn get-albums [{:keys [flickr] :as ctx}]
  (let [user-id (.. (:auth flickr) getUser getId)]
    (-&gt;&gt; (-&gt; (:client flickr)
             (.getPhotosetsInterface)
             (.getList user-id)
             (.getPhotosets))
         (map (partial -&gt;album ctx)))))
</code></pre><h2 id="making_off_with_the_goods">Making off with the goods</h2><p>Now that we have a way to list albums and the photos within them, let&#39;s see how to download said photos. Reading on in <code>Backup.java</code>, I encounter this good stuff:</p><pre><code>Photo p = (Photo) setIterator.next();
String url = p.getLargeUrl();
URL u = new URL(url);
String filename = u.getFile();
filename = filename.substring(filename.lastIndexOf(&#34;/&#34;) + 1, filename.length());
System.out.println(&#34;Now writing &#34; + filename + &#34; to &#34; + setDirectory.getCanonicalPath());
BufferedInputStream inStream = new BufferedInputStream(photoInt.getImageAsStream(p, Size.LARGE));
File newFile = new File(setDirectory, filename);

FileOutputStream fos = new FileOutputStream(newFile);

int read;

while ((read = inStream.read()) != -1) {
    fos.write(read);
}
fos.flush();
fos.close();
inStream.close();
</code></pre><p>That looks like one way to do it. ðŸ˜¬</p><p>Let&#39;s see if we can clean this up a little. This whole mess:</p><pre><code>while ((read = inStream.read()) != -1) {
    fos.write(read);
}
</code></pre><p>can be replaced with <a href="https://clojuredocs.org/clojure.java.io/copy">clojure.java.io/copy</a>, so let&#39;s update the <code>ns</code> form to require in <code>clojure.java.io</code>, and import the other Java classes mentioned in this code snippet whilst we&#39;re at it:</p><pre><code>(ns clickr.flickr
  (:require [babashka.fs :as fs]
            [clojure.java.io :as io])
  (:import (com.flickr4java.flickr Flickr
                                   RequestContext
                                   REST)
           (com.flickr4java.flickr.auth Permission)
           (com.flickr4java.flickr.photos Size)
           (com.flickr4java.flickr.util FileAuthStore)
           (java.io BufferedInputStream
                    FileOutputStream)))
</code></pre><p>I also find this stuff to get the filename of the photo to be a bit annoying:</p><pre><code>String url = p.getLargeUrl();
URL u = new URL(url);
String filename = u.getFile();
filename = filename.substring(filename.lastIndexOf(&#34;/&#34;) + 1, filename.length());
</code></pre><p>Let&#39;s make the filename the photo&#39;s ID plus its format. We can do this in our <code>-&gt;photo</code> function like so:</p><pre><code>(defn -&gt;photo [_ photo]
  (let [id (.getId photo)
        extension (.getOriginalFormat photo)
        filename (format &#34;%s.%s&#34; id extension)]
    {:id id
     :filename filename
     :title (.getTitle photo)
     :description (.getDescription photo)
     :date-taken (.getDateTaken photo)
     :width (.getOriginalWidth photo)
     :height (.getOriginalHeight photo)
     :geo-data (.getGeoData photo)
     :rotation (.getRotation photo)
     :object photo}))
</code></pre><p>Now we can write a function to download a photo nicely. To be good citizens, let&#39;s put the file in the tmp directory (<code>/tmp</code> on Linux and MacOS, who knows where on Windows). Luckily for us, babashka.fs has a handy <code>temp-dir</code> function that can do this! ðŸŽ‰</p><pre><code>(defn download-photo! [{:keys [flickr] :as ctx}
                       {:keys [filename] :as photo}]
  (let [p-interface (.getPhotosInterface (:client flickr))]
    (with-open [in (BufferedInputStream. (.getImageAsStream p-interface (:object photo) Size/LARGE))
                out (FileOutputStream. (fs/file (fs/temp-dir) filename))]
      (io/copy in out))))
</code></pre><p>Now let&#39;s try calling it and seeing if it works!</p><pre><code>(comment

  (def album (-&gt; (get-albums ctx) first))
  ;; =&gt; #&#39;clickr.flickr/album

  (def photo (-&gt; album :photos first))
  ;; =&gt; #&#39;clickr.flickr/photo

  photo
  ;; =&gt; {:description nil,
  ;;     :date-taken nil,
  ;;     :geo-data nil,
  ;;     :rotation -1,
  ;;     :width 0,
  ;;     :title &#34;sean-hargreaves-phoenix-new-5-final-a&#34;,
  ;;     :filename &#34;53460147147.jpg&#34;,
  ;;     :id &#34;53460147147&#34;,
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photos.Photo 0x5d288375 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;],
  ;;     :height 0}

  (download-photo! ctx photo)
  ;; =&gt; nil

  (fs/exists? (fs/file (fs/temp-dir) (:filename photo)))
  ;; =&gt; true

  )
</code></pre><p>Sure enough, we now have a <code>/tmp/53460147147.jpg</code> file!</p><p><img src="https://jmglov.net/blog/assets/2024-01-17-phoenix.jpg" alt="A space station" title="The Phoenix rises!" width="800px"/></p><h2 id="backing_things_up">Backing things up</h2><p>Now that we&#39;ve proven that we can download a photo, let&#39;s think about how we want the backup process to work. What we probably want is to create a &#34;folder&#34; in our S3 bucket for each album, and then put all of the photos for that album inside it. Let&#39;s write a function to download an entire album.</p><p>The first order of business is to create a directory to hold the photos in the album that we&#39;re about to download. Let&#39;s use the album ID as the name of the directory and drop it in the tmp directory:</p><pre><code>(comment

  (fs/file (fs/temp-dir) (:id album))
  ;; =&gt; #object[java.io.File 0x4e8e72c2 &#34;/tmp/72177720314024335&#34;]

  )
</code></pre><p>We can now use <code>create-dirs</code> to create the directory:</p><pre><code>
(comment

  (-&gt;&gt; (fs/file (fs/temp-dir) (:id album)) fs/create-dirs)
  ;; =&gt; #object[sun.nio.fs.UnixPath 0x1fbb7d68 &#34;/tmp/72177720314024335&#34;]

  )
</code></pre><p>OK, so we have a directory to hold the photos, but we&#39;re going to have to update <code>download-photo!</code> to use that directory instead of dropping stuff straight into <code>/tmp</code>. Easy enough:</p><pre><code>(defn download-photo! [{:keys [flickr out-dir] :as ctx}
                       {:keys [filename] :as photo}]
  (let [p-interface (.getPhotosInterface (:client flickr))]
    (with-open [in (BufferedInputStream. (.getImageAsStream p-interface (:object photo) Size/LARGE))
                out (FileOutputStream. (fs/file out-dir filename))]
      (io/copy in out))))

(comment

  (def album-dir (-&gt;&gt; (fs/file (fs/temp-dir) (:id album)) fs/create-dirs))
  ;; =&gt; #&#39;clickr.flickr/album-dir

  (download-photo! (assoc ctx :out-dir album-dir) photo)
  ;; =&gt; nil

  (fs/exists? (fs/file album-dir (format &#34;%s.jpg&#34; (:id photo))))
  ;; =&gt; true

  )
</code></pre><p>Having thus tamed <code>download-photo!</code>, we can write <code>download-album!</code>:</p><pre><code>(defn download-album! [ctx {:keys [id photos] :as album}]
  (let [album-dir (-&gt;&gt; id (fs/file (fs/temp-dir)) fs/create-dirs fs/file)]
    (-&gt;&gt; photos
         (map (partial download-photo! (assoc ctx :out-dir album-dir)))
         doall)))

(comment

  (download-album! ctx album)
  ;; =&gt; (nil nil nil nil nil nil nil nil)

  (fs/glob album-dir &#34;*&#34;)
  ;; =&gt; [#object[sun.nio.fs.UnixPath 0x72466b2f &#34;/tmp/72177720314024335/53461405604.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x788d40e5 &#34;/tmp/72177720314024335/53460163402.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x26f7ff12 &#34;/tmp/72177720314024335/53460161007.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x250e8e92 &#34;/tmp/72177720314024335/53460147147.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x17fa6f15 &#34;/tmp/72177720314024335/53461214223.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x5e28f67 &#34;/tmp/72177720314024335/53461091151.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x1cf4ffa6 &#34;/tmp/72177720314024335/53460151727.jpg&#34;]
  ;;     #object[sun.nio.fs.UnixPath 0x4ddf88b4 &#34;/tmp/72177720314024335/53461088046.jpg&#34;]]

  )
</code></pre><p>Fantastic! Though I have to admit that I don&#39;t find the list of <code>nil</code>s very friendly. Let&#39;s make one last change to <code>download-photo</code> so that it returns the photo, with the location of the downloaded file added to it:</p><pre><code>(defn download-photo! [{:keys [flickr out-dir] :as ctx}
                       {:keys [filename] :as photo}]
  (let [p-interface (.getPhotosInterface (:client flickr))
        out-file (fs/file out-dir filename)]
    (with-open [in (BufferedInputStream. (.getImageAsStream p-interface (:object photo) Size/LARGE))
                out (FileOutputStream. out-file)]
      (io/copy in out))
    (assoc photo :out-file out-file)))

(comment

  (download-photo! (assoc ctx :out-dir album-dir) photo)
  ;; =&gt; {:description nil,
  ;;     :date-taken nil,
  ;;     :geo-data nil,
  ;;     :rotation -1,
  ;;     :width 0,
  ;;     :title &#34;sean-hargreaves-phoenix-new-5-final-a&#34;,
  ;;     :filename &#34;53460147147.jpg&#34;,
  ;;     :id &#34;53460147147&#34;,
  ;;     :out-file
  ;;     #object[java.io.File 0x44f5abfa &#34;/tmp/72177720314024335/53460147147.jpg&#34;],
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photos.Photo 0x4d361782 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;],
  ;;     :height 0}

  )
</code></pre><p>Much nicer! We can now do the same thing to <code>download-album!</code>:</p><pre><code>(defn download-album! [ctx {:keys [id photos] :as album}]
  (let [album-dir (-&gt;&gt; id (fs/file (fs/temp-dir)) fs/create-dirs fs/file)
        photos (-&gt;&gt; photos
                    (map (partial download-photo! (assoc ctx :out-dir album-dir)))
                    doall)]
    (assoc album :out-dir album-dir, :photos photos)))

(comment

  (download-album! ctx album)
  ;; =&gt; {:id &#34;72177720314024335&#34;,
  ;;     :title &#34;clickr demo&#34;,
  ;;     :description &#34;Photo album demo for my clickr blog post&#34;,
  ;;     :photos
  ;;     ({:description nil,
  ;;       :date-taken nil,
  ;;       :geo-data nil,
  ;;       :rotation -1,
  ;;       :width 0,
  ;;       :title &#34;sean-hargreaves-phoenix-new-5-final-a&#34;,
  ;;       :filename &#34;53460147147.jpg&#34;,
  ;;       :id &#34;53460147147&#34;,
  ;;       :out-file
  ;;       #object[java.io.File 0x163fa069 &#34;/tmp/72177720314024335/53460147147.jpg&#34;],
  ;;       :object
  ;;       #object[com.flickr4java.flickr.photos.Photo 0x4d361782 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;],
  ;;       :height 0}
  ;; [...]
  ;;      {:description nil,
  ;;       :date-taken nil,
  ;;       :geo-data nil,
  ;;       :rotation -1,
  ;;       :width 0,
  ;;       :title &#34;daniel-jennings-img-7554&#34;,
  ;;       :filename &#34;53460151727.jpg&#34;,
  ;;       :id &#34;53460151727&#34;,
  ;;       :out-file
  ;;       #object[java.io.File 0x289e2344 &#34;/tmp/72177720314024335/53460151727.jpg&#34;],
  ;;       :object
  ;;       #object[com.flickr4java.flickr.photos.Photo 0x805e360 &#34;com.flickr4java.flickr.photos.Photo@436e36e8&#34;],
  ;;       :height 0}),
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photosets.Photoset 0x19d70721 &#34;com.flickr4java.flickr.photosets.Photoset@19d70721&#34;],
  ;;     :out-dir #object[java.io.File 0x67b12ec4 &#34;/tmp/72177720314024335&#34;]}

  )
</code></pre><h2 id="uploading_to_s3">Uploading to S3</h2><p>We&#39;re most of the way there now. The final piece of the puzzle is taking our lovely directory full of downloaded files and putting it on S3, as described above. To accomplish this, let&#39;s avail ourselves of the Cognitect <a href="https://github.com/cognitect-labs/aws-api">aws-api</a> library that has <a href="https://jmglov.net/blog/tags/aws.html">served us
so well in the past</a>. First, let&#39;s add it to <code>deps.edn</code>:</p><pre><code>{:paths [&#34;src&#34; &#34;dev&#34;]
 :deps {babashka/fs {:mvn/version &#34;0.4.19&#34;}
        com.cognitect.aws/api {:mvn/version &#34;0.8.686&#34;}
        com.cognitect.aws/endpoints {:mvn/version &#34;1.1.12.504&#34;}
        com.cognitect.aws/s3 {:mvn/version &#34;848.2.1413.0&#34;}
        com.flickr4java/flickr4java {:mvn/version &#34;3.0.1&#34;}}}
</code></pre><p>Sadly, we&#39;ll now need to restart our REPL. I really need to get hot reloading working! Sounds like a project for another day, though I&#39;m sure it will be <a href="https://jmglov.net/blog/tags/50-simple-steps.html">incredibly simple</a>...</p><p>In any case, having now restarted our REPL, let&#39;s create a <code>clickr.s3</code> namespace for ourselves:</p><pre><code>(ns clickr.s3
  (:require [cognitect.aws.client.api :as aws]
            [clojure.string :as str]
            [clojure.java.io :as io])
  (:import (java.io ByteArrayInputStream)))
</code></pre><p>Now we&#39;ll need an S3 client. Let&#39;s follow the same pattern we used for our Flickr client:</p><pre><code>(defn init-client [{:keys [aws-region] :as ctx}]
  (let [client (aws/client {:api :s3, :region aws-region})]
    (assoc ctx :s3 {:client client})))
</code></pre><p>Now we can actually use the same config as we used before! ðŸ¤¯ We just need to add the AWS region in there, then we can call <code>init-client</code>:</p><pre><code>
(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;
               :aws-region &#34;eu-west-1&#34;})
  ;; =&gt; #&#39;clickr.s3/config

  (def ctx (init-client config))
  ;; =&gt; #&#39;clickr.s3/ctx

  (-&gt; ctx :s3 :client)
  ;; =&gt; #object[cognitect.aws.client.impl.Client 0x6b5a7704 &#34;cognitect.aws.client.impl.Client@6b5a7704&#34;]

  )
</code></pre><p>With a working client in hand, we can write a function to upload a photo. Let&#39;s follow the same pattern as <code>download-photo!</code>: do the side-effecting thing and then return the photo, assoc-ing in the S3 key where we uploaded it.</p><pre><code>(defn upload-photo! [{:keys [s3 s3-bucket] :as ctx}
                     {:keys [out-file] :as photo}]
  (let [s3-key :???]
    (aws/invoke (:client s3)
                {:op :PutObject
                 :request {:Bucket s3-bucket
                           :Key s3-key
                           :Body :???}})
    (assoc photo :s3-key s3-key)))
</code></pre><p>OK, a couple things here. First, we need to add the S3 bucket to our context. That&#39;s easy enough:</p><pre><code>
(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;
               :aws-region &#34;eu-west-1&#34;
               :s3-bucket &#34;photos.jmglov.net&#34;})
  ;; =&gt; #&#39;clickr.s3/config

  (def ctx (init-client config))
  ;; =&gt; #&#39;clickr.s3/ctx

  ctx
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :aws-region &#34;eu-west-1&#34;,
  ;;     :s3-bucket &#34;photos.jmglov.net&#34;,
  ;;     :s3
  ;;     {:client
  ;;      #object[cognitect.aws.client.impl.Client 0xcd319cb &#34;cognitect.aws.client.impl.Client@cd319cb&#34;]}}

  )
</code></pre><p>Next, we need a way to read in the photo file. babashka.fs comes to the rescue again with a function called <code>read-all-bytes</code>! Let&#39;s require in babashka.fs:</p><pre><code>(ns clickr.s3
  (:require [babashka.fs :as fs]
            [cognitect.aws.client.api :as aws]))
</code></pre><p>And now we can use that in our <code>upload-photo!</code> function:</p><pre><code>(defn upload-photo! [{:keys [s3 s3-bucket] :as ctx}
                     {:keys [out-file] :as photo}]
  (let [s3-key :???]
    (aws/invoke (:client s3)
                {:op :PutObject
                 :request {:Bucket s3-bucket
                           :Key s3-key
                           :Body (fs/read-all-bytes out-file)}})
    (assoc photo :s3-key s3-key)))
</code></pre><p>Finally, we need to figure out what the S3 key should be. Using the convention from <code>download-album!</code>, we know the photo will be in a directory corresponding to the album ID. Let&#39;s use that as the key and prepend <code>clickr/</code> to it so that we don&#39;t pollute the top-level of the S3 bucket with a bunch of nonsense. We can grab the directory and filename using our old friend babashka.fs, of course:</p><pre><code>(comment

  (def photo {:out-file &#34;/tmp/72177720314024335/53460151727.jpg&#34;})
  ;; =&gt; #&#39;clickr.s3/photo

  (-&gt; photo :out-file fs/file-name)
  ;; =&gt; &#34;53460151727.jpg&#34;

  (-&gt; photo :out-file fs/parent fs/file-name)
  ;; =&gt; &#34;72177720314024335&#34;

  )
</code></pre><p>Cool! With this, we have enough to construct the S3 key:</p><pre><code>(defn upload-photo! [{:keys [s3 s3-bucket] :as ctx}
                     {:keys [out-file] :as photo}]
  (let [s3-key (format &#34;%s/%s/%s&#34;
                       &#34;clickr&#34;
                       (-&gt; photo :out-file fs/parent fs/file-name)
                       (-&gt; photo :out-file fs/file-name))]
    (aws/invoke (:client s3)
                {:op :PutObject
                 :request {:Bucket s3-bucket
                           :Key s3-key
                           :Body (fs/read-all-bytes out-file)}})
    (assoc photo :s3-key s3-key)))
</code></pre><p>We can try this out:</p><pre><code>(comment

  (upload-photo! ctx photo)
  ;; =&gt; {:out-file &#34;/tmp/72177720314024335/53460151727.jpg&#34;,
  ;;     :s3-key &#34;clickr/72177720314024335/53460151727.jpg&#34;}

  )
</code></pre><p>And one should always trust but verify, right?</p><pre><code>: jmglov@laurana; aws s3 ls s3://photos.jmglov.net/clickr/72177720314024335/53460151727.jpg
2024-01-17 12:06:17      90743 53460151727.jpg
</code></pre><p>Wow, that was surprisingly painless!</p><p>One thing that&#39;s bothering me a little, though, is that hardcoded &#34;clickr&#34; in there. Let&#39;s move that to the config like we did with the S3 bucket:</p><pre><code>(defn upload-photo! [{:keys [s3 s3-bucket s3-prefix] :as ctx}
                     {:keys [out-file] :as photo}]
  (let [s3-key (format &#34;%s/%s/%s&#34;
                       s3-prefix
                       (-&gt; photo :out-file fs/parent fs/file-name)
                       (-&gt; photo :out-file fs/file-name))]
    (aws/invoke (:client s3)
                {:op :PutObject
                 :request {:Bucket s3-bucket
                           :Key s3-key
                           :Body (fs/read-all-bytes out-file)}})
    (assoc photo :s3-key s3-key)))

(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;
               :aws-region &#34;eu-west-1&#34;
               :s3-bucket &#34;photos.jmglov.net&#34;
               :s3-prefix &#34;clickr&#34;})
  ;; =&gt; #&#39;clickr.s3/config

  (def ctx (init-client config))
  ;; =&gt; #&#39;clickr.s3/ctx

  ctx
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :aws-region &#34;eu-west-1&#34;,
  ;;     :s3-bucket &#34;photos.jmglov.net&#34;,
  ;;     :s3-prefix &#34;clickr&#34;,
  ;;     :s3
  ;;     {:client
  ;;      #object[cognitect.aws.client.impl.Client 0x2dc1f1c2 &#34;cognitect.aws.client.impl.Client@2dc1f1c2&#34;]}}

  (upload-photo! ctx photo)
  ;; =&gt; {:out-file &#34;/tmp/72177720314024335/53460151727.jpg&#34;,
  ;;     :s3-key &#34;clickr/72177720314024335/53460151727.jpg&#34;}

  )
</code></pre><p>Having done this, writing a function to upload all the photos in an album is quite straightforward:</p><pre><code>(defn upload-album! [ctx {:keys [photos] :as album}]
  (update album :photos #(doall (map (partial upload-photo! ctx) %))))

(comment

  (def album-dir &#34;/tmp/72177720314024335&#34;)
  ;; =&gt; #&#39;clickr.s3/album-dir

  (def album {:id &#34;72177720314024335&#34;
              :out-dir album-dir
              :photos (-&gt;&gt; (fs/glob album-dir &#34;*&#34;)
                           (map (fn [out-file] {:out-file (str out-file)})))})
  ;; =&gt; #&#39;clickr.s3/album

  (upload-album! ctx album)
  ;; =&gt; {:id &#34;72177720314024335&#34;,
  ;;     :out-dir &#34;/tmp/72177720314024335&#34;,
  ;;     :photos
  ;;     ({:out-file &#34;/tmp/72177720314024335/53461405604.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53461405604.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53460163402.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460163402.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53460161007.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460161007.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53460147147.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460147147.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53461214223.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53461214223.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53461091151.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53461091151.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53460151727.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460151727.jpg&#34;}
  ;;      {:out-file &#34;/tmp/72177720314024335/53461088046.jpg&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53461088046.jpg&#34;})}

  )
</code></pre><h2 id="tying_it_all_together">Tying it all together</h2><p>All of this looks reasonably reasonable, but our REPL-driven development of the <code>clickr.s3</code> namespace was pretty mocktacular, which fills me with a vague sense of unease. Let&#39;s make sure we can upload an honest to goodness album!</p><p>To make sure we don&#39;t have any cruft lying around in our REPL, let&#39;s create a new namespace and require in the flickr and s3 stuff:</p><pre><code>(ns user
  (:require [clickr.flickr :as flickr]
            [clickr.s3 :as s3]))
</code></pre><p>We&#39;ll need some config, which we can just copy and paste straight from our experiments in the s3 namespace:</p><pre><code>(comment

  (def config {:api-key &#34;beefface5678910&#34;
               :secret &#34;facecafe1234&#34;
               :aws-region &#34;eu-west-1&#34;
               :s3-bucket &#34;photos.jmglov.net&#34;
               :s3-prefix &#34;clickr&#34;})
  ;; =&gt; #&#39;user/config

  )
</code></pre><p>With this config firmly in hand, we can create flickr and s3 clients:</p><pre><code>(comment

  (def ctx (-&gt; config flickr/init-client s3/init-client))
  ;; =&gt; #&#39;user/ctx

  ctx
  ;; =&gt; {:api-key &#34;beefface5678910&#34;,
  ;;     :secret &#34;facecafe1234&#34;,
  ;;     :aws-region &#34;eu-west-1&#34;,
  ;;     :s3-bucket &#34;photos.jmglov.net&#34;,
  ;;     :s3-prefix &#34;clickr&#34;,
  ;;     :flickr
  ;;     {:client
  ;;      #object[com.flickr4java.flickr.Flickr 0x2c4181a2 &#34;com.flickr4java.flickr.Flickr@2c4181a2&#34;],
  ;;      :auth-store
  ;;      #object[com.flickr4java.flickr.util.FileAuthStore 0x5368861c &#34;com.flickr4java.flickr.util.FileAuthStore@5368861c&#34;],
  ;;      :auth
  ;;      #object[com.flickr4java.flickr.auth.Auth 0x7c8ff0d6 &#34;com.flickr4java.flickr.auth.Auth@7c8ff0d6&#34;]},
  ;;     :s3
  ;;     {:client
  ;;      #object[cognitect.aws.client.impl.Client 0x5b35646d &#34;cognitect.aws.client.impl.Client@5b35646d&#34;]}}

  )
</code></pre><p>Download an album:</p><pre><code>(comment

  (def album (-&gt;&gt; (flickr/get-albums&#39; ctx)
                  first
                  (flickr/download-album!&#39; ctx)))
  ;; =&gt; #&#39;user/album

  (-&gt;&gt; album :photos (map :out-file))
  ;; =&gt; (#object[java.io.File 0x1c2cd5ae &#34;/tmp/72177720314024335/53460147147.jpg&#34;]
  ;;     #object[java.io.File 0x1678a01 &#34;/tmp/72177720314024335/53461405604.jpg&#34;]
  ;;     #object[java.io.File 0x4f708220 &#34;/tmp/72177720314024335/53461091151.jpg&#34;]
  ;;     #object[java.io.File 0x75723234 &#34;/tmp/72177720314024335/53461088046.jpg&#34;]
  ;;     #object[java.io.File 0x6cace2fd &#34;/tmp/72177720314024335/53460163402.jpg&#34;]
  ;;     #object[java.io.File 0x51265aeb &#34;/tmp/72177720314024335/53460161007.jpg&#34;]
  ;;     #object[java.io.File 0x1a0ca36d &#34;/tmp/72177720314024335/53461214223.jpg&#34;]
  ;;     #object[java.io.File 0x7e8bc18a &#34;/tmp/72177720314024335/53460151727.jpg&#34;])

  )
</code></pre><p>And upload it to S3!</p><pre><code>(comment

  (s3/upload-album! ctx album)
  ;; =&gt; {:id &#34;72177720314024335&#34;,
  ;;     :title &#34;clickr demo&#34;,
  ;;     :description &#34;Photo album demo for my clickr blog post&#34;,
  ;;     :photos
  ;;     ({:description nil,
  ;;       :date-taken nil,
  ;;       :geo-data nil,
  ;;       :rotation -1,
  ;;       :width 0,
  ;;       :title &#34;sean-hargreaves-phoenix-new-5-final-a&#34;,
  ;;       :filename &#34;53460147147.jpg&#34;,
  ;;       :id &#34;53460147147&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460147147.jpg&#34;,
  ;;       :out-file
  ;;       #object[java.io.File 0x1c2cd5ae &#34;/tmp/72177720314024335/53460147147.jpg&#34;],
  ;;       :object
  ;;       #object[com.flickr4java.flickr.photos.Photo 0x3bbff9e1 &#34;com.flickr4java.flickr.photos.Photo@14ea992b&#34;],
  ;;       :height 0}
  ;; [...]
  ;;      {:description nil,
  ;;       :date-taken nil,
  ;;       :geo-data nil,
  ;;       :rotation -1,
  ;;       :width 0,
  ;;       :title &#34;daniel-jennings-img-7554&#34;,
  ;;       :filename &#34;53460151727.jpg&#34;,
  ;;       :id &#34;53460151727&#34;,
  ;;       :s3-key &#34;clickr/72177720314024335/53460151727.jpg&#34;,
  ;;       :out-file
  ;;       #object[java.io.File 0x7e8bc18a &#34;/tmp/72177720314024335/53460151727.jpg&#34;],
  ;;       :object
  ;;       #object[com.flickr4java.flickr.photos.Photo 0x1e20bd4c &#34;com.flickr4java.flickr.photos.Photo@436e36e8&#34;],
  ;;       :height 0}),
  ;;     :object
  ;;     #object[com.flickr4java.flickr.photosets.Photoset 0x6411aa2d &#34;com.flickr4java.flickr.photosets.Photoset@6411aa2d&#34;],
  ;;     :out-dir #object[java.io.File 0x377eb990 &#34;/tmp/72177720314024335&#34;]}

  )
</code></pre><p>And check that all is right with the world:</p><pre><code>: jmglov@laurana; aws s3 ls s3://photos.jmglov.net/clickr/72177720314024335/
2024-01-17 12:48:26     125643 53460147147.jpg
2024-01-17 12:48:29      90743 53460151727.jpg
2024-01-17 12:48:28      88417 53460161007.jpg
2024-01-17 12:48:28     185725 53460163402.jpg
2024-01-17 12:48:27     178392 53461088046.jpg
2024-01-17 12:48:27     106074 53461091151.jpg
2024-01-17 12:48:29      88013 53461214223.jpg
2024-01-17 12:48:26      98035 53461405604.jpg
</code></pre><p><img src="https://jmglov.net/blog/assets/2023-11-11-victory.jpg" alt="A woman on a beach at sunrise with her head thrown back, saying " victory""="" title="Victory!" width="800px]"/></p><p>But wait just a second here...</p><h2 id="the_unbearable_lightness_of_being_dissatisfied">The unbearable lightness of being dissatisfied</h2><p><img src="https://jmglov.net/blog/assets/2024-01-17-sadness.jpg" alt="A person sitting alone, holding their face in their hands" title="Sadness!" width="800px]"/></p><p>Much like Angelica Schuyler, I&#39;ll never be satisfied, because having to browse my albums with the S3 console is kind of a let down, not to mention that this:</p><p><img src="https://jmglov.net/blog/assets/2024-01-17-album-flickr.png" alt="The Flickr site, displaying the photos in the clickr demo album" title="OMG ðŸ¤©" width="800px]"/></p><p>looks way better than this:</p><p><img src="https://jmglov.net/blog/assets/2024-01-17-s3-console.png" alt="The AWS S3 console, displaying the files we uploaded as a table of text" title="Sadness!" width="800px]"/></p><p>What is a young man to do? Well, you&#39;ll have to stick around for the next instalment of <a href="https://jmglov.net/blog/tags/clickr.html">this exciting series</a>, which I promise I&#39;ll actually write, because I&#39;ve started writing it already and... um... trust me?</p><p>Part 2: <a href="https://jmglov.net/blog/2024-01-22-clickr-goes-fe.html">clickr goes frontend</a></p>

<p>Discuss this post <a href="https://clojurians.slack.com/archives/C8NUSGWG6/p1705498559454789">here</a>.</p>

<p><i>Published: 2024-01-17</i></p>

<p>
  <i>
  Tagged:
  
  <span>
    <a href="https://jmglov.net/blog/tags/clojure.html">clojure</a>
  </span>
  
  <span>
    <a href="https://jmglov.net/blog/tags/aws.html">aws</a>
  </span>
  
  <span>
    <a href="https://jmglov.net/blog/tags/clickr.html">clickr</a>
  </span>
  
  </i>
</p>



      
      
      
    </div></div>
  </body>
</html>
