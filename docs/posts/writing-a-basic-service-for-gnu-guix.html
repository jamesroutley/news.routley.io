<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://tannerhoelzel.com/gnu-shepherd-simple-service.html">Original</a>
    <h1>Writing a basic service for GNU Guix</h1>
    
    <div id="readability-page-1" class="page"><div id="content">


<p>
Let&#39;s write a service for GNU Guix that will automatically start
<a href="https://github.com/kmonad/kmonad">kmonad</a> at boot, and keep it running until shutdown.
</p>

<p>
Note: before we proceed, we could make a kmonad &#34;daemon&#34; without
going to the trouble of writing a Guix-specific service: we could
start kmonad in a shell profile, write a cron job, or something
else. The user experience of these methods might be the same, but
writing a service keeps things organized and seems like the &#34;right&#34;
way. If nothing else, it&#39;s a great learning opportunity.
</p>

<div id="outline-container-orgd3a063f">
<h2 id="orgd3a063f">Reading the docs</h2>
<div id="text-orgd3a063f">
<p>
First let&#39;s read the documentation of <a href="https://guix.gnu.org/en/manual/en/guix.html">GNU Guix</a>. We see that Guix
offers &#34;services&#34; which extend the functionality of the operating
system. The manual says:
</p>

<blockquote>
<p>
Guix system services are connected by extensions. For instance, the
secure shell service extends the Shepherd—the initialization system,
running as PID 1—by giving it the command lines to start and stop the
secure shell daemon (see openssh-service-type); the UPower service
extends the D-Bus service by passing it its .service specification,
and extends the udev service by passing it device management rules
(see upower-service); the Guix daemon service extends the Shepherd by
passing it the command lines to start and stop the daemon, and extends
the account service by passing it a list of required build user
accounts (see Base Services).
</p>
</blockquote>

<p>
The Guix daemon sounds very similar to what we&#39;re looking to
construct. Nearby, we see that the Guix daemon defines its own
&#34;service type&#34;:
</p>

<div>
<pre>(define guix-service-type
  (service-type
   (name &#39;guix)
   (extensions
    (list (service-extension shepherd-root-service-type guix-shepherd-service)
          (service-extension account-service-type guix-accounts)
          (service-extension activation-service-type guix-activation)))
   (default-value (guix-configuration))))
</pre>
</div>

<p>
Hm. I didn&#39;t expect we&#39;d need to instatiate our own data type just to
create a simple init daemon. After all, kmonad is simpler than the
guix daemon - it just needs to start at boot, and that&#39;s it. The
documentation is terse and it&#39;s not clear whether there&#39;s a better way
to proceed. Maybe if we find a simple example of service, we can build
off of it.
</p>
</div>
</div>

<div id="outline-container-org63d37f4">
<h2 id="org63d37f4">Reading some source</h2>
<div id="text-org63d37f4">
<p>
I saw that &#34;Game Services&#34; were listed in the manual. Maybe there&#39;s a
simple one we can make a template of. First, we find the definition of
the games services <a href="https://git.savannah.gnu.org/cgit/guix.git/tree/gnu/services/games.scm"><kbd>/gnu/services/games.scm</kbd></a>:
</p>

<div>
<pre>;;; GNU Guix --- Functional package management for GNU
;;; Copyright © 2018 Arun Isaac &lt;arunisaac@systemreboot.net&gt;
;;;
;;; This file is part of GNU Guix.
;;;
;;; GNU Guix is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; GNU Guix is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with GNU Guix.  If not, see &lt;http://www.gnu.org/licenses/&gt;.

(define-module (gnu services games)
  #:use-module (gnu services)
  #:use-module (gnu services shepherd)
  #:use-module (gnu packages admin)
  #:use-module (gnu packages games)
  #:use-module (gnu system shadow)
  #:use-module (guix gexp)
  #:use-module (guix modules)
  #:use-module (guix records)
  #:use-module (ice-9 match)
  #:export (wesnothd-configuration
            wesnothd-configuration?
            wesnothd-service-type))

;;;
;;; The Battle for Wesnoth server
;;;

(define-record-type* &lt;wesnothd-configuration&gt;
  wesnothd-configuration make-wesnothd-configuration wesnothd-configuration?
  (package wesnothd-configuration-package
           (default wesnoth-server))
  (port wesnothd-configuration-port
        (default 15000)))

(define %wesnothd-accounts
  (list (user-account
         (name &#34;wesnothd&#34;)
         (group &#34;wesnothd&#34;)
         (system? #t)
         (comment &#34;Wesnoth daemon user&#34;)
         (home-directory &#34;/var/empty&#34;)
         (shell (file-append shadow &#34;/sbin/nologin&#34;)))
        (user-group
         (name &#34;wesnothd&#34;)
         (system? #t))))

(define wesnothd-shepherd-service
  (match-lambda
    (($ &lt;wesnothd-configuration&gt; package port)
     (with-imported-modules (source-module-closure
                             &#39;((gnu build shepherd)))
       (shepherd-service
        (documentation &#34;The Battle for Wesnoth server&#34;)
        (provision &#39;(wesnoth-daemon))
        (requirement &#39;(networking))
        (modules &#39;((gnu build shepherd)))
        (start #~(make-forkexec-constructor/container
                  (list #$(file-append package &#34;/bin/wesnothd&#34;)
                        &#34;-p&#34; #$(number-&gt;string port))
                  #:user &#34;wesnothd&#34; #:group &#34;wesnothd&#34;))
        (stop #~(make-kill-destructor)))))))

(define wesnothd-service-type
  (service-type
   (name &#39;wesnothd)
   (description
    &#34;Run The Battle for Wesnoth server @command{wesnothd}.&#34;)
   (extensions
    (list (service-extension account-service-type
                             (const %wesnothd-accounts))
          (service-extension shepherd-root-service-type
                             (compose list wesnothd-shepherd-service))))
   (default-value (wesnothd-configuration))))
</pre>
</div>

<p>
There&#39;s only one game contained in <kbd>(gnu services games)</kbd> and that is
The Battle for Wesnoth Server. The <a href="https://guix.gnu.org/manual/en/html_node/Game-Services.html">Game Services reference</a> says
</p>

<blockquote>
<p>
To run wesnothd in the default configuration, instantiate it as:
</p>

<p>
<kbd>(service wesnothd-service-type)</kbd>
</p>
</blockquote>

<p>
So far, we have a module that exports <kbd>wesnothd-service-type</kbd> and a
way to use that binding in our system config to declare a service.
Apparently, a new <kbd>service-type</kbd> is what we&#39;re looking for after
all. Let&#39;s start by implementing a <kbd>kmonad-service-type</kbd> and see where
that takes us.
</p>
</div>
</div>

<div id="outline-container-orgabb1195">
<h2 id="orgabb1195">Writing <kbd>kmonad-service-type</kbd></h2>
<div id="text-orgabb1195">
<p>
A <kbd>service-type</kbd> (see <a href="https://guix.gnu.org/manual/en/html_node/Service-Reference.html">Service Reference</a>) takes a name, description,
and a list of <kbd>service-extension</kbd> objects, each of which describes how
to extend existing services. So what exactly is a
<kbd>&lt;service-extension&gt;</kbd> object? The <kbd>service-extension</kbd> documentation
says:
</p>

<blockquote>
<p>
Scheme Procedure: <kbd>service-extension</kbd> target-type compute
</p>

<p>
Return a new extension for services of type target-type. compute must
be a one-argument procedure: <kbd>fold-services</kbd> calls it, passing it the
value associated with the service that provides the extension; it must
return a valid value for the target service.
</p>
</blockquote>

<p>
So in the context of <kbd>(define kmonad-service-type ...)</kbd>, a <kbd>compute</kbd>
function will receive the (single) value associated with
<kbd>kmonad-service-type</kbd> (a value which we have yet to define) and will
return the value required by <kbd>target-type</kbd>, which is the service being
extended. The <kbd>wesnothd-service-type</kbd> extends two services: the
<a href="https://guix.gnu.org/manual/en/html_node/Service-Types-and-Services.html"><kbd>account-service-type</kbd></a> which is extended by a list of <kbd>user-group</kbd> and
<kbd>user-account</kbd> objects; and the <a href="https://guix.gnu.org/manual/en/html_node/Shepherd-Services.html"><kbd>shepherd-root-service-type</kbd></a> which is
extended by a list of <kbd>&lt;shepherd-service&gt;</kbd> objects.
</p>

<p>
So which services should kmonad extend? It&#39;s recommended to make a
dedicated user and group for a daemon (see <a href="https://unix.stackexchange.com/questions/29159/why-is-it-recommended-to-create-a-group-and-user-for-some-applications">stackoverflow</a>), so let&#39;s
extend the <kbd>account-service-type</kbd> with a new user and group to run the
kmonad daemon. Let&#39;s also extend the <kbd>shepherd-root-service-type</kbd>
because we want our daemon managed by the init system, Shepherd:
</p>

<div>
<pre>(define kmonad-service-type
  (service-type
   (name &#39;kmonad)
   (description
    &#34;Run kmonad as a daemon.&#34;)
   (extensions
    (list (service-extension account-service-type
                             (const %kmonad-daemon-accounts))
          (service-extension shepherd-root-service-type
                             (compose list kmonad-shepherd-service))))))
</pre>
</div>

<p>
It looks pretty similar to <kbd>wesnothd-service-type</kbd>, but without the
<kbd>default-value</kbd>, which doesn&#39;t really apply to kmonad. Next, we&#39;ve got
to choose how to extend the <kbd>account-service-type</kbd> and the
<kbd>shepherd-root-service-type</kbd>.
</p>
</div>

<div id="outline-container-org68615dd">
<h3 id="org68615dd">Extending the <kbd>account-service-type</kbd></h3>
<div id="text-org68615dd">
<p>
As noted above, <kbd>(const %kmonad-daemon-accounts)</kbd> needs to evaluate to a
function of a single argument that returns a list of users and
groups. Well, <kbd>const</kbd> makes a function that takes some argument, and
then just returns whatever was passed to <kbd>const</kbd>–in this case,
<kbd>%kmonad-daemon-accounts</kbd>. All we&#39;ve got to do is define
<kbd>%kmonad-daemon-accounts</kbd>.
</p>

<p>
We need one daemon user and one group. Our daemon user won&#39;t require
login, so we&#39;ll use <kbd>/sbin/nologin</kbd> (see <kbd>man 8 nologin</kbd>) like
<kbd>%wesnothd-accounts</kbd>. Similarly, it won&#39;t require a home directory. We
can pretty much copy the <kbd>%wesnothd-accounts</kbd> to make
<kbd>%kmonad-daemon-accounts</kbd>:
</p>

<div>
<pre>(define %kmonad-daemon-accounts
  (list (user-account
         (name &#34;kmonad-daemon&#34;)
         (group &#34;kmonad-daemon&#34;)
         (system? #t)
         (comment &#34;kmonad daemon user&#34;)
         (home-directory &#34;/var/empty&#34;)
         (shell (file-append shadow &#34;/sbin/nologin&#34;)))
        (user-group
         (name &#34;kmonad-daemon&#34;)
         (system? #t))))
</pre>
</div>
</div>
</div>

<div id="outline-container-orgf8ce49b">
<h3 id="orgf8ce49b">Extending the <kbd>shepherd-root-service-type</kbd></h3>
<div id="text-orgf8ce49b">
<p>
As noted above, <kbd>(compose list kmonad-shepherd-service)</kbd> needs to
evaluate to a function of a single argument (a single argument of our
choice) and return a list of <kbd>&lt;shepherd-service&gt;</kbd> objects. That means
that <kbd>kmonad-shepherd-service</kbd> must take a single argument, and return
a single <kbd>shepherd-service</kbd>! Before we write
<kbd>kmonad-shepherd-service</kbd>, let&#39;s decide what we want to pass
it. Minimally, kmonad needs a <kbd>.kbd</kbd> file to run. In theory, a user
could also specify other stuff (e.g. a log level), but let&#39;s just
start with the <kbd>.kbd</kbd> path.
</p>

<p>
If we&#39;re using <kbd>wesnoth-shepherd-service</kbd> as an example, we&#39;ll need to
think about the <kbd>requirement</kbd>, <kbd>start</kbd>, and <kbd>stop</kbd> fields.
</p>
</div>

<div id="outline-container-org2666c50">
<h4 id="org2666c50"><kbd>requirement</kbd> field</h4>
<div id="text-org2666c50">
<p>
We definitely need to wait for <kbd>udev</kbd>, but maybe we should also wait
for <kbd>user-processes</kbd> like the other daemons in the shepherd graph
do. Looking at the <a href="https://git.savannah.gnu.org/cgit/guix.git/tree/gnu/services/shepherd.scm">source</a> for <kbd>user-processes</kbd>:
</p>

<blockquote>
<p>
This is a synchronization point used to make sure user processes and daemons
get started only after crucial initial services have been started—file
system mounts, etc.  This is similar to the &#39;sysvinit&#39; target in systemd.
</p>
</blockquote>

<p>
In theory we could explicitly wait for other shepherd services, but it
seems like this catch-all applies perfectly to our use case. Come to
think of it, I wonder why <kbd>wesnothd</kbd> doesn&#39;t depend on
<kbd>user-processes</kbd> too.
</p>
</div>
</div>

<div id="outline-container-org51b5d91">
<h4 id="org51b5d91"><kbd>start</kbd> and <kbd>stop</kbd> fields</h4>
<div id="text-org51b5d91">
<p>
According to the Shepherd Services <a href="https://guix.gnu.org/manual/en/html_node/Shepherd-Services.html">documentation</a>, the <kbd>start</kbd> and
<kbd>stop</kbd> fields of <kbd>shepherd-service</kbd> take <a href="https://guix.gnu.org/manual/en/html_node/G_002dExpressions.html">G-Expressions</a>. But what&#39;s a
g-expression? Well, because Guix uses Scheme for both higher-level
actions–like defining packages–and lower-level actions–like
building derivations generated by packages– it needs a faculty for
embedding lower-level code in higher-level code. So in the <kbd>start</kbd>
field of <kbd>wesnoth-shepherd-service</kbd>:
</p>

<div>
<pre>(start #~(make-forkexec-constructor/container
          (list #$(file-append package &#34;/bin/wesnothd&#34;)
                &#34;-p&#34; #$(number-&gt;string port))
          #:user &#34;wesnothd&#34; #:group &#34;wesnothd&#34;))
</pre>
</div>

<p>
Some lower-level code is passed with <kbd>#~(...)</kbd>, within which
higher-level code is escaped with <kbd>#$(...)</kbd> which the compiler is able
to &#34;lower&#34; to lower-level code. Looking at the <a href="https://git.savannah.gnu.org/cgit/guix.git/tree/gnu/build/shepherd.scm">source</a> for
<kbd>make-forkexec-constructor/container</kbd>, we see:
</p>

<blockquote>
<p>
This is a variant of &#39;make-forkexec-constructor&#39; that starts COMMAND in
NAMESPACES, a list of Linux namespaces such as &#39;(mnt ipc).  MAPPINGS is the
list of &lt;file-system-mapping&gt; to make in the case of a separate mount
namespace, in addition to essential bind-mounts such /proc.
</p>
</blockquote>

<p>
And the <a href="https://www.gnu.org/software/shepherd/manual/html_node/Service-De_002d-and-Constructors.html#Service-De_002d-and-Constructors">documentation</a> for <kbd>make-forkexec-contructor</kbd> reads:
</p>

<blockquote>
<p>
Return a procedure that forks a child process, closes all file
descriptors except the standard output and standard error descriptors,
sets the current directory to directory, sets the umask to
file-creation-mask unless it is #f, changes the environment to
environment-variables (using the environ procedure), sets the current
user to user and the current group to group unless they are #f, and
executes command (a list of strings.) The result of the procedure will
be the PID of the child process. Note that this will not work as
expected if the process “daemonizes” (forks); in that case, you will
need to pass #:pid-file, as explained below.
</p>

<p>
When pid-file is true, it must be the name of a PID file associated
with the process being launched; the return value is the PID once that
file has been created. If pid-file does not show up in less than
pid-file-timeout seconds, the service is considered as failing to
start.
</p>

<p>
When log-file is true, it names the file to which the service’s
standard output and standard error are redirected. log-file is created
if it does not exist, otherwise it is appended to.
</p>
</blockquote>

<p>
The <kbd>wesnoth</kbd> source appears not to use the <kbd>NAMESPACES</kbd> feature of
the containerized-version, so we&#39;ll stick with
<kbd>make-forkexec-constructor</kbd> as is used in the <a href="https://guix.gnu.org/manual/en/html_node/Shepherd-Services.html">syslogd example</a>.
</p>
</div>
</div>

<div id="outline-container-orgd64f772">
<h4 id="orgd64f772"><kbd>kmonad-shepherd-service</kbd></h4>
<div id="text-orgd64f772">
<p>
Let&#39;s put it all together:
</p>

<div>
<pre>(define (kmonad-shepherd-service kbd-path)
  (shepherd-service
   (documentation &#34;Run the kmonad daemon (kmonad-daemon).&#34; )
   (provision &#39;(kmonad-daemon))
   (requirement &#39;(udev user-processes))
   (start #~(make-forkexec-constructor
             (list #$(file-append kmonad &#34;/bin/kmonad&#34;)
                   #$kbd-path &#34;-l info&#34;)
             #:user &#34;kmonad-daemon&#34; #:group &#34;kmonad-daemon&#34;
             #:log-file &#34;/var/log/kmonad.log&#34;))
   (stop #~(make-kill-destructor))))
</pre>
</div>
</div>
</div>
</div>

<div id="outline-container-org5a3eaba">
<h3 id="org5a3eaba">Making a module</h3>
<div id="text-org5a3eaba">
<p>
Now let&#39;s package our code into a module. If the location where we
keep local guix modules is <kbd>~/local-guix</kbd>, then we can add our module
at <kbd>~/local-guix/my/services/kmonad.scm</kbd> as:
</p>

<div>
<pre>(define-module (my services kmonad)
  #:use-module (gnu services)
  #:use-module (gnu services shepherd)
  #:use-module (gnu packages haskell-apps)
  #:use-module (gnu system shadow)
  #:use-module (guix gexp)
  #:export (kmonad-service-type))

(define %kmonad-daemon-accounts
  (list (user-account
         (name &#34;kmonad-daemon&#34;)
         (group &#34;kmonad-daemon&#34;)
         (system? #t)
         (comment &#34;kmonad daemon user&#34;)
         (home-directory &#34;/var/empty&#34;)
         (shell (file-append shadow &#34;/sbin/nologin&#34;)))
        (user-group
         (name &#34;kmonad-daemon&#34;)
         (system? #t))))

(define (kmonad-shepherd-service kbd-path)
  (shepherd-service
   (documentation &#34;Run the kmonad daemon (kmonad-daemon).&#34; )
   (provision &#39;(kmonad-daemon))
   (requirement &#39;(udev user-processes))
   (start #~(make-forkexec-constructor
             (list #$(file-append kmonad &#34;/bin/kmonad&#34;)
                   #$kbd-path &#34;-l info&#34;)
             #:user &#34;kmonad-daemon&#34; #:group &#34;kmonad-daemon&#34;
             #:log-file &#34;/var/log/kmonad.log&#34;))
   (stop #~(make-kill-destructor))))

(define kmonad-service-type
  (service-type
   (name &#39;kmonad)
   (description
    &#34;Run kmonad as a daemon.&#34;)
   (extensions
    (list (service-extension account-service-type
                             (const %kmonad-daemon-accounts))
          (service-extension shepherd-root-service-type
                             (compose list kmonad-shepherd-service))))))
</pre>
</div>
</div>
</div>
</div>

<div id="outline-container-org62db731">
<h2 id="org62db731">Using <kbd>kmonad-service-type</kbd></h2>
<div id="text-org62db731">
<p>
Now adding a kmonad service is straightforward. Let&#39;s add some other
kmonad-specific configuration to our system while we&#39;re at it:
</p>

<div>
<pre>(use-modules (my services kmonad))
;; more modules

(operating-system
  (users
    (append (list (user-account
                    (supplementary-groups
                      &#39;(&#34;input&#34; ;; needed by kmonad
                        ;; more groups
                       ))
                    ;; more fields
                  )
                  ;; more users
            )
            %base-user-accounts))
  (packages
    (append (list
             &#34;kmonad&#34;
             ;; more packages
            )
            %base-packages))
  (services
    (append (list (service kmonad-service-type &#34;/path/to/config.kbd&#34;)
                  ;; more services
            )
            (modify-services %desktop-services ;; needed to add kmonad udev rules
              (udev-service-type config =&gt;
                (udev-configuration (inherit config)
                  (rules (cons kmonad
                    (udev-configuration-rules config))))))))
  ;; more fields
)
</pre>
</div>

<p>
And reconfigure:
</p>

<div>
<pre>sudo guix system -L ~/local-guix reconfigure /path/to/config.scm
</pre>
</div>
</div>
</div>

<div id="outline-container-org316b41e">
<h2 id="org316b41e">A simpler daemon?</h2>
<div id="text-org316b41e">
<p>
I think it would be possible to add kmonad as a <kbd>simple-service</kbd> on top of
<kbd>shepherd-root-service-type</kbd>. After looking at the <kbd>simple-service</kbd>
<a href="https://git.savannah.gnu.org/cgit/guix.git/tree/gnu/services.scm">source</a>, we can try:
</p>

<div>
<pre>(simple-service &#39;kmonad-service shepherd-root-service-type
                (list (shepherd-service
                       (documentation &#34;Run the kmonad daemon (kmonad-daemon).&#34; )
                       (provision &#39;(kmonad-daemon))
                       (requirement &#39;(udev user-processes))
                       (start #~(make-forkexec-constructor
                                 (list #$(file-append kmonad &#34;/bin/kmonad&#34;)
                                       #$kbd-path &#34;-l info&#34;)
                                 #:log-file &#34;/var/log/kmonad.log&#34;))
                       (stop #~(make-kill-destructor)))))

</pre>
</div>
</div>
</div>


</div></div>
  </body>
</html>
