<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://www.evalapply.org/posts/emerging-from-dotemacs-bankruptcy-midway-refactor/index.html">Original</a>
    <h1>Emerging from dotemacs bankruptcy the hard way: the midway refactor</h1>
    
    <div id="readability-page-1" class="page"><div id="the-very-top"><main id="main"><article id="blog-post"><header><div><p>Emerging from dotemacs bankruptcy the hard way: the midway refactor</p><p>Or the one in which we confront our elisp n00bishness and try to be better at using it. And we learn new habits to understand our Emacs better. Better late than never.</p><hr/></div></header><section><p>Part way through &#34;the plan&#34;, I realised a few things:</p><ul><li>I had been learning elisp by osmosis. I needed to make some of it explicit.</li><li>A subtle bug or two had crept in.</li><li>Better code layout and organisation would ease my life.</li><li>I should have added more &#34;better defaults&#34; already.</li></ul><p>Areas for improvement presented themselves, such as purpose and usage of <code>setq</code>, <code>put</code>, <code>setq-default</code>. So I started using <code>M-x describe-*</code> a lot. I wish I had habituated to this when I was new to Emacs. If you are just starting off, wiring <code>describe-*</code> into muscle memory early on is a good idea. It will help you:</p><ul><li>demystify what your Emacs is made of and how it works under the hood</li><li>learn to use what you have, with expertise (keybindings, modes, features etc.)</li><li>gain confidence to improve, debug, and tailor Emacs to your needs</li><li>discover useful and creative ways to do more things with Emacs</li></ul><p><code>describe-*</code> s I have used frequently while developing the dotemacs:</p><pre><code>describe-variable
describe-function
describe-command
describe-package
</code></pre><p>Other <code>describe-*</code> s I have used so far:</p><pre><code>describe-mode
describe-key
describe-keymap
describe-bindings
describe-face
describe-font
</code></pre><p>And when I don&#39;t know what&#39;s what in the first place, <code>apropos</code> comes to the rescue. Learn that too!</p><p>I also enriched help with the <code>helpful</code> package, discovered in <a href="https://systemcrafters.net/emacs-from-scratch/helpful-ui-improvements/">episode 2 of <em>Emacs from Scratch</em></a> by systemcrafters.net.</p><pre><code>(use-package helpful ; h/t systemcrafters.net
  ;; https://github.com/Wilfred/helpful
  :ensure t
  :bind ((&#34;C-h f&#34; . #&#39;helpful-callable)
         (&#34;C-h F&#34; . #&#39;helpful-function) ; exclude macros
         (&#34;C-h v&#34; . #&#39;helpful-variable)
         (&#34;C-h k&#34; . #&#39;helpful-key)
         (&#34;C-h x&#34; . #&#39;helpful-command)
         ;; Lookup the current symbol at point. C-c C-d is
         ;; a common keybinding for this in lisp modes.
         (&#34;C-c C-d&#34; . #&#39;helpful-at-point)))
</code></pre><p>Another thing I discovered was that I had created subtle bugs by not knowing Directory v/s File naming conventions. Directory names should always be correctly terminated, so they are interpreted and handled correctly. So I started using <code>file-name-as-directory</code> since.</p><pre><code>(defvar dotemacs-savefile-dir
  (file-name-as-directory
   (expand-file-name &#34;savefile&#34; dotemacs-dir))
  &#34;This folder stores all the automatically generated save/history-files.&#34;)

(unless (file-exists-p dotemacs-savefile-dir)
  (make-directory dotemacs-savefile-dir))
</code></pre><p>Pre-debugging, the savefile dir was <code>(expand-file-name &#34;savefile&#34; dotemacs-dir)</code>, and that broke package configuration statements that tried to create savefiles.</p><p>On similar lines, I have noticed several dotemacsen in the wild use <code>concat</code> to construct paths. That seems more error-prone (bad path construction), and not portable, say, if the same dotemacs needs work on Linux and on Windows. Presumably the many inbuilt file and directory manipulation functions exist to handle the many platform specific oddities for us. I think it&#39;s wise to look these up for anything to do with file handling (<a href="https://www.gnu.org/software/emacs/manual/html_node/emacs/Files.html">see manual</a>). Some useful ones are:</p><pre><code>file-exists-p
expand-file-name
file-name-as-directory
make-directory
make-empty-file
load-file
load-file-name
</code></pre><p>On another level, now that more code was being added, the conceptual layout of said code felt ripe for cleanup. I also had a better sense of how and why to do this by dint of having read other peoples layout choices. So, many globals were consolidated and some more added a la &#34;better defaults&#34;. This is where neater use of `setq` and `setq-default` helped.</p><p>And as a <em>pièce de résistance</em>, I also chose to constrain my Emacs to <code>v28.1</code> or higher. Now that this stable version is widely available, I don&#39;t anticipate trouble in any of my personal or work environments.</p><p>The init.el so far (<a href="https://unix.stackexchange.com/questions/emerging-from-dotemacs-bankruptcy-packages/index.html#main">previously</a>, <a href="https://unix.stackexchange.com/questions/emerging-from-dotemacs-bankruptcy-init-begins/index.html#main">even earlier</a>)…</p><details><summary><code>After the mid-way refactor, and some more additions</code><hr/></summary><pre><code>;;; package --- init.el  -*- lexical-binding: t -*- --- My Emacs configuration.

;;; Commentary:

;;; This file is not part of GNU Emacs.

;;; Author: Aditya Athalye
;;; Created on: 30 June 2023
;;; Copyright (c) 2023 Aditya Athalye

;;; License:
;;; This program is free software; you can redistribute it and/or
;;; modify it under the terms of the MIT license, which is included
;;; with this distribution. See the LICENCE.txt file.

;;; Code:

;; Set the low bar Emacs compatibility high
(defvar dotemacs-min-version &#34;28.1&#34;)

(when (version&lt; emacs-version dotemacs-min-version)
  (error &#34;We demand spiffy new Emacs, at least v%s, but you have v%s&#34;
         dotemacs-min-version
         emacs-version))

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; DIRECTORY STRUCTURE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Take clues from bbatsov/prelude, except keep structure relative to our
;; initial dotemacs-dir path. This way we can start the user&#39;s emacs via
;; ~/.emacs.d symlinked to the dotemacs repo, and develop/debug against
;; the repo without potentially overwriting transient state files of the
;; daily driver .emacs.d.

(defvar dotemacs-dir
  (file-name-directory (or load-file-name (buffer-file-name)))
  &#34;The dotemacs&#39; root.  Normally it should be ~/.emacs.d.&#34;)

(defvar dotemacs-custom-file (expand-file-name &#34;custom.el&#34; dotemacs-dir)
  &#34;Make Emacs add customisations here, instead of the init file.
Usually customisations made from the UI go into `custom-file&#39;.&#34;)
(setq custom-file dotemacs-custom-file)
(unless (file-exists-p dotemacs-custom-file)
  (make-empty-file dotemacs-custom-file))
(load-file custom-file) ; load *now*, instead of unpredictable load sequence

(defvar dotemacs-savefile-dir (file-name-as-directory
                               (expand-file-name &#34;savefile&#34; dotemacs-dir))
  &#34;This folder stores all the automatically generated save/history-files.&#34;)
(unless (file-exists-p dotemacs-savefile-dir)
  (make-directory dotemacs-savefile-dir))

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; BETTER DEFAULTS
;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; via
;; - technomancy/better-defaults
;; - bbatsov/prelude
;; - vedang/emacs-up
;; - suvratapte/dot-emacs-dot-d

;; SPACES OVER TABS,
;; but keep up conventional appearances of 8 character wide tabs.
;; NOTE: Use `C-q TAB` to insert literal tabs.
(setq-default indent-tabs-mode nil
              tab-width 8
              fill-column 80)

(setq
 ;; PERFORMANCE
 ;; gc ~100MB for better overall performance. ~50-100MB is recommended
 ;; these days over the long-obsolete default of ~8MB.
 gc-cons-threshold (* 100 1024 1024)
 ;; Large files freeze Emacs. Warn for files over ~100MB.
 large-file-warning-threshold (* 100 1024 1024)
 ;; Always load newest byte code. cf. bbatsov/prelude
 load-prefer-newer t

 ;; INTERACTIONS
 inhibit-startup-message t
 ring-bell-function &#39;ignore ; no beeps
 require-final-newline t ; always well-form files
 confirm-kill-emacs &#39;y-or-n-p ; instead of disabling &#39;C-x C-c&#39;
 create-lockfiles nil
 tab-always-indent &#39;complete
 tab-first-completion &#39;word
 uniquify-buffer-name-style &#39;forward
 uniquify-after-kill-buffer-p t ; rename after killing uniquified
 uniquify-ignore-buffers-re &#34;^\\*&#34;) ; spare special buffers

;; MORE INTERACTIONS
;; http://ergoemacs.org/emacs/emacs_save_restore_opened_files.html
(global-auto-revert-mode 1) ; auto-revert buffer if file-on-disk changes
(delete-selection-mode t) ; delete selection for any keypress
(add-hook &#39;before-save-hook &#39;delete-trailing-whitespace)
(fset &#39;yes-or-no-p &#39;y-or-n-p) ; typing &#34;yes/no&#34; gets annoying fast
(global-unset-key (kbd &#34;C-z&#34;)) ; avoid fat fingering `suspend-frame&#39;

;; UTF-8 as default encoding
;; ref: http://ergoemacs.org/emacs/emacs_encoding_decoding_faq.html
(set-language-environment &#34;UTF-8&#34;)

;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; VISUAL AESTHETICS
;; ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Go easy on the eyes. This high-contrast darkmode theme is built
;; into Emacs, as of Emacs version 28.1
(load-theme &#39;modus-vivendi)

;; More screen real estate
(scroll-bar-mode 0)
(tool-bar-mode 0)
(tooltip-mode 0) ; disable popup, make help text appear in echo area
(menu-bar-mode 0)
(column-number-mode t)
(set-fringe-mode &#39;(5 . 13)) ;; describe variable fringe-mode
(global-display-line-numbers-mode 1) ; always show line numbers
(global-hl-line-mode +1)

;; Tweak Font sizes globally, and also set line number mode
(defun adi/set-frame-font--default ()
  &#34;Interactively set default frame font for day to day work.&#34;
  (interactive)
  (set-frame-font &#34;-PfEd-DejaVu Sans Mono-normal-normal-normal-*-13-*-*-*-m-0-iso10646-1&#34;)
  (global-display-line-numbers-mode -1))

(defun adi/set-frame-font--pair-prog ()
  &#34;Interactively set frame font for pair programming.&#34;
  (interactive)
  (set-frame-font &#34;-PfEd-DejaVu Sans Mono-normal-normal-normal-*-16-*-*-*-m-0-iso10646-1&#34;)
  (global-display-line-numbers-mode 1))

(defun adi/set-frame-font--code-demo ()
  &#34;Interactively set frame font for presentations and demos.&#34;
  (interactive)
  (set-frame-font &#34;-1ASC-Liberation Mono-normal-normal-normal-*-28-*-*-*-m-0-iso10646-1&#34;)
  (global-display-line-numbers-mode -1))

;; Ensure we always start Emacs with the default font.
(adi/set-frame-font--default)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Package management
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(require &#39;package)
;; Explicitly set the exact package archives list
(setq package-archives &#39;((&#34;melpa&#34; . &#34;https://melpa.org/packages/&#34;)
                         (&#34;org&#34; . &#34;https://orgmode.org/elpa/&#34;)
                         (&#34;elpa&#34; . &#34;https://elpa.gnu.org/packages/&#34;)))
;; Set package download directory relative to the dotemacs-dir
(setq package-user-dir (file-name-as-directory
                        (expand-file-name &#34;elpa&#34; dotemacs-dir)))

(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Use use-package
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Ian Y.E. Pan&#39;s tutorial is a nice quick overview.
;; https://ianyepan.github.io/posts/setting-up-use-package/

(unless (package-installed-p &#39;use-package)
  (package-install &#39;use-package))

(eval-when-compile
  (add-to-list &#39;load-path package-user-dir)
  (require &#39;use-package))

(setq use-package-always-ensure t)
(setq use-package-expand-minimally t) ; set nil to debug use-package forms

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; All the packages!
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Packages useful to configure packages

;; radian-software/blackout to tune major/minor mode names
;; in modeline. It unifies functionality of the mutually
;; confusing modeline lighters delight, diminish, and dim.
;; It integrates seamlessly with use-package.
(use-package blackout)

(use-package helpful ; h/t systemcrafters.net
  ;; https://github.com/Wilfred/helpful
  :ensure t
  :bind ((&#34;C-h f&#34; . #&#39;helpful-callable)
         (&#34;C-h F&#34; . #&#39;helpful-function) ; exclude macros
         (&#34;C-h v&#34; . #&#39;helpful-variable)
         (&#34;C-h k&#34; . #&#39;helpful-key)
         (&#34;C-h x&#34; . #&#39;helpful-command)
         ;; Lookup the current symbol at point. C-c C-d is
         ;; a common keybinding for this in lisp modes.
         (&#34;C-c C-d&#34; . #&#39;helpful-at-point)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; General text viewing and editing
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Enable narrowings to enhance focus, and reduce accidental
;; edits of nonfocus areas (thanks to save-restrictions).
;; h/t bbatsov/prelude
;; Note: `C-x n w` makes all visible again.
(put &#39;narrow-to-region &#39;disabled nil)
(put &#39;narrow-to-page &#39;disabled nil)
(put &#39;narrow-to-defun &#39;disabled nil)

;; COMplete ANYthing, please!
;; TODO: instead try radian&#39;s completion system
;;; ref: https://github.com/radian-software/radian
(use-package company
  :bind (:map global-map
              (&#34;TAB&#34; . company-indent-or-complete-common)
              ;; Got to love the name hippie-expand. Use this for general
              ;; expansions, because company-complete is good for the
              ;; current/narrow-case expansions. By default, `M-/` binds
              ;; to the less powerful `dabbrev-expand`. To alter search
              ;; options, :config the hippie-expand-try-functions-list.
              (&#34;M-/&#34; . hippie-expand))
  :config (setq company-idle-delay 0.1
                company-minimum-prefix-length 2)
  (global-company-mode t)
  :blackout)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Lispy editing support
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Tweak settings of built-in paren package
(use-package paren
  :ensure nil                    ; it already exists, don&#39;t try to search online
  :init
  (setq show-paren-delay 0)
  :config
  (show-paren-mode t)
  :blackout)

(use-package paredit
  ;; Handy configs available at the wiki https://www.emacswiki.org/emacs/ParEdit
  ;; including combining with eldoc, &#34;electric&#34; enabled modes etc.
  :ensure t
  :bind
  ((&#34;M-[&#34; . paredit-wrap-square)
   (&#34;M-{&#34; . paredit-wrap-curly))
  :hook ((emacs-lisp-mode lisp-interaction-mode)
         . paredit-mode)
  :blackout)

(use-package magit
  :diminish)

(provide &#39;init)
  ;;; init.el ends here
</code></pre></details><p>Our current place in the rough plan:</p><ul><li>[✓] Set the very preliminaries.</li><li>[✓] Set up package management. I&#39;ll probably stick with the old familiars; elpa and melpa. I&#39;m not sure about straight.el at this time.</li><li>[✓] Choose <code>use-package</code> to get and configure each package. I like how neat configs are, when defined with use-package.</li><li>[✓] Unexpectedly refactor the whole thing.</li><li>[WIP] Make completions and &#34;getting about&#34; work (the right mix of ivy, consul, swiper, company, helm, imenu). Someone mentioned newer alternatives to helm. Have a look at that.</li><li>[WIP] Fix general text editing stuff (keybindings, multiple cursors, snippets etc.)</li><li>[WIP] Add support for favourite programming languages.<ul><li>[✓] Emacs Lisp</li><li>many others…</li></ul></li><li>org-mode specifics</li><li>then let&#39;s see…</li></ul><p>Although not quite… This post is for posterity. By now, I have done a fair bit of work on the &#34;getting about&#34; bits of the plan, viz. navigation, movement, window layout, UI state, discoverability and so forth. That is for the next post.</p></section></article></main></div></div>
  </body>
</html>
