<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://infinitedigits.co/tinker/thx/?utm_source=atom_feed">Original</a>
    <h1>THX theme in SuperCollider</h1>
    
    <div id="readability-page-1" class="page"><div id="maincontent">
            
<p><img src="https://infinitedigits.co/img/thxlogo.jpg"/>
</p>
<div>
    
    
    
    
    <p>
            <postamble datetime="2024-03-24" 2018-09-04=""><time datetime="2024-03-24 08:00:46">March 24th, 2024</time></postamble>
         / <a href="https://infinitedigits.co/tags/software">#software</a> 
    </p>
    <p>An epic sound in just a few lines of code.</p>
</div>
<p>The THX “Deep Note” was composed by Lucasfilm sound engineer Dr. James Moorer and premiered in 1983 at the debut of “Star Wars: Episode VI.” The original was written in 20,000 lines of C code. However, as a demonstration of SuperCollider, I found it engaging to program in fewer than 100 lines.</p>
<h2 id="the-score">the score</h2>
<p>The score itself is a remarkably concise piece of music, shown below in its entirity.</p>
<p><a href="https://infinitedigits.co/img/thx.png"><img src="https://infinitedigits.co/img/thx.png" alt="THX score"/></a></p>
<p>It provides all necessary details for reconstruction:</p>
<ol>
<li>Use three detuned voices for high notes and two for low notes.</li>
<li>Generate random pitches between 200 and 400 Hz.</li>
<li>Oscillate pitches for approximately 10 seconds.</li>
<li>Approach final pitch over approximately 6 seconds.</li>
<li>Hold final pitches for 24 seconds.</li>
<li>Gradually increase volume until the end.</li>
</ol>
<p>Let’s try!</p>
<h2 id="the-final-pitches">the final pitches</h2>
<p>Lets start at the end, rather than the beginning to make things easier. The final notes, from the score, are D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6. We can gather all these notes as midi notes - <code>[26, 38, 45, 50, 57, 62, 69, 74, 81, 86, 90]</code> and create a little Routine in SuperCollider that will play a “thx” synth (to be determined). But here we are:</p>
<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({  
  Routine {
    // sync the server
    s.sync;
    // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
    ~thxNotes = [26, 38, 45, 50, 57, 62, 69, 74, 81, 86, 90];
    // play all notes
    Synth.tail(s,&#34;out&#34;);
    ~thxNotes.do({ arg v;
      if (v&lt;60,{
        // 2 voices
        Synth(&#34;thx&#34;,[\noteFinal,v]);
        Synth(&#34;thx&#34;,[\noteFinal,v]);
      },{
        // 3 voices, slightly detuneds
        Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
        Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
        Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
      });
    });
  }.play;
});
)
</code></pre><p>For notes less than middle C (midi note “60”) there are two voices created, and for the higher notes there are three voices with randomization added to their <code>noteFinal</code>.</p>
<h2 id="the-basic-synth">the basic synth</h2>
<p>Now lets make a basic synth that plays these pitches. For now lets just make a super basic “thx” synth:</p>

<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({
   SynthDef(&#34;thx&#34;,{
      arg noteFinal=72;
      var snd;

      // set random inital note
      var note = noteFinal;

      snd = Saw.ar(note.midicps);
      snd = snd * EnvGen.ar(Env.new([0,1],[1]));
      Out.ar(0,snd*12.neg.dbamp);
   }).add;

   Routine {
      // sync the server
      s.sync;
      // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
      ~thxNotes = [26,38,45,50,57,62,69,74,81,86,90];
      // play all notes
      Synth.tail(s,&#34;out&#34;);
      ~thxNotes.do({ arg v;
         if (v&lt;60,{
            // 2 voices
            Synth(&#34;thx&#34;,[\noteFinal,v]);
            Synth(&#34;thx&#34;,[\noteFinal,v]);
         },{
            // 3 voices, slightly detuneds
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
         });
      });

   }.play;
});
)
</code></pre><h2 id="random-pitches">random pitches</h2>
<p>To define some initial notes that transition into the final “THX” notes, you can generate random pitches and then smoothly transition them to the final notes. Here’s how you can do it in SuperCollider:</p>

<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({
   SynthDef(&#34;thx&#34;,{
      arg noteFinal=72;
      var snd;
      
      // set random inital note
      var noteInitial = Rand(200,400).cpsmidi;      
      
      // note movement
      var noteMove = EnvGen.ar(Env.new([0,1],[8],curve:\sine));
      
      // setting up the note
      var note = noteInitial;
      // add the movement in the note
      note = note + (noteMove* (noteFinal-noteInitial));
      
      snd = Saw.ar(note.midicps);
      snd = snd * EnvGen.ar(Env.new([0,1],[1]));
      Out.ar(0,snd*12.neg.dbamp);
   }).add;
   
   Routine {
      // sync the server
      s.sync;
      // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
      ~thxNotes = [26,38,45,50,57,62,69,74,81,86,90];
      // play all notes
      Synth.tail(s,&#34;out&#34;);
      ~thxNotes.do({ arg v;
         if (v&lt;60,{
            // 2 voices
            Synth(&#34;thx&#34;,[\noteFinal,v]);
            Synth(&#34;thx&#34;,[\noteFinal,v]);
         },{
            // 3 voices, slightly detuneds
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
         });
      });
      
   }.play;
});
)
</code></pre><h2 id="random-pitch-movement">random pitch movement</h2>
<p>the score is a little more subtle than this though - as each note seems to be able tow wave around a little bit before the actual movement begins to the final note. lets add this in too. we will introduce <code>noteRandomization</code>, which when it is finished will trigger the <code>noteMove</code> envelope.</p>

<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({
   SynthDef(&#34;thx&#34;,{
      arg noteFinal=72;
      var snd;
      
      // set random inital note
      var noteInitial = Rand(200,400).cpsmidi;      
      
      // note randomization
      var noteRandomization = EnvGen.ar(Env.new([1,1,0],[11,1],curve:\welch));
      
      // note movement
      var noteMove = EnvGen.ar(Env.new([0,1],[8],curve:\sine),gate:noteRandomization&lt;0.01);
      
      // setting up the note
      var note = noteInitial;
      // add the randomization of the note
      note = note + (noteRandomization * LFNoise2.kr(1).range(-1,1));
      // add the movement in the note
      note = note + (noteMove* (noteFinal-noteInitial));
      
      snd = Saw.ar(note.midicps);
      snd = snd * EnvGen.ar(Env.new([0,1],[1]));
      Out.ar(0,snd*12.neg.dbamp);
   }).add;
   
   Routine {
      // sync the server
      s.sync;
      // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
      ~thxNotes = [26,38,45,50,57,62,69,74,81,86,90];
      // play all notes
      Synth.tail(s,&#34;out&#34;);
      ~thxNotes.do({ arg v;
         if (v&lt;60,{
            // 2 voices
            Synth(&#34;thx&#34;,[\noteFinal,v]);
            Synth(&#34;thx&#34;,[\noteFinal,v]);
         },{
            // 3 voices, slightly detuneds
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
         });
      });
      
   }.play;
});
)
</code></pre><h2 id="envelopes">envelopes!</h2>
<p>envelopes can control the volume of the sound, and that is the last thing that we are missing. we need an envelope to increase the volume over time, so we can use <code>EnvGen</code> with something like:</p>
<pre tabindex="0"><code data-lang="supercollider">snd = snd * EnvGen.ar(Env.new([-36,-16,-4,-4,-96],[8,8,8,2]),doneAction:2,timeScale:timeScale).dbamp;
</code></pre><p>which will increase and then decrease at the end.</p>

<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({
   SynthDef(&#34;thx&#34;,{
      arg noteFinal=72;
      var snd;
      
      // set random inital note
      var noteInitial = Rand(200,400).cpsmidi;      
      
      // note randomization
      var noteRandomization = EnvGen.ar(Env.new([1,1,0],[11,1],curve:\welch));
      
      // note movement
      var noteMove = EnvGen.ar(Env.new([0,1],[8],curve:\sine),gate:noteRandomization&lt;0.01);
      
      // setting up the note
      var note = noteInitial;
      // add the randomization of the note
      note = note + (noteRandomization * LFNoise2.kr(1).range(-1,1));
      // add the movement in the note
      note = note + (noteMove* (noteFinal-noteInitial));
         
      snd = Saw.ar(note.midicps);
            
      // slowly louder
      snd = snd * EnvGen.ar(Env.new([-36,-16,-4,-4,-96],[8,8,8,2]),doneAction:2).dbamp;
      
      snd = snd * EnvGen.ar(Env.new([0,1],[1]));
      Out.ar(0,snd*12.neg.dbamp);
   }).add;
   
   Routine {
      // sync the server
      s.sync;
      // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
      ~thxNotes = [26,38,45,50,57,62,69,74,81,86,90];
      // play all notes
      Synth.tail(s,&#34;out&#34;);
      ~thxNotes.do({ arg v;
         if (v&lt;60,{
            // 2 voices
            Synth(&#34;thx&#34;,[\noteFinal,v]);
            Synth(&#34;thx&#34;,[\noteFinal,v]);
         },{
            // 3 voices, slightly detuneds
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
         });
      });
      
   }.play;
});
)
</code></pre><h2 id="thats-about-it">thats about it!</h2>
<p>and that’s about it! now its about just adding a few elements to spruce things up a bit. we can add a reverb stage at the end and we can also add some random panning to the final code and get something like this:</p>

<pre tabindex="0"><code data-lang="supercollider">(
s.waitForBoot({
   SynthDef(&#34;thx&#34;,{
      arg noteFinal=72;
      var timeScale = 1;
      var snd;

      // set random inital note
      var noteInitial = Rand(200,400).cpsmidi;

      // note randomization
      var noteRandomization = EnvGen.ar(Env.new([1,1,0],[11,1],curve:\welch),timeScale:timeScale);

      // note movement
      var noteMove = EnvGen.ar(Env.new([0,1],[8],curve:\sine),gate:noteRandomization&lt;0.01,timeScale:timeScale);

      // setting up the note
      var note = noteInitial;
      // add the randomization of the note
      note = note + (noteRandomization * LFNoise2.kr(1).range(-1,1));
      // add the movement in the note
      note = note + (noteMove* (noteFinal-noteInitial));

      // sawtooth oscillator
      snd = Saw.ar(note.midicps);

      // some low-pass filtering
      snd = LPF.ar(snd,Rand(90,130).midicps);

      // random panning
      snd = Pan2.ar(snd,Rand(-0.5,0.5));

      // slowly louder
      snd = snd * EnvGen.ar(Env.new([-36,-16,-4,-4,-96],[8,8,8,2]),doneAction:2,timeScale:timeScale).dbamp;

      snd = snd * EnvGen.ar(Env.new([0,1],[1]));
      Out.ar(0,snd*12.neg.dbamp);
   }).add;


   // output
   SynthDef(&#34;out&#34;,{
      var snd = In.ar(0,2);
      var sndr;
      var snd2 = snd;
      // reverb
      snd2 = DelayN.ar(snd2, 0.03, 0.03);
      snd2 = DelayN.ar(snd2, 0.15, 0.15);
      snd2 = CombN.ar(snd2, 0.1, {Rand(0.01,0.099)}!32, 4);
      snd2 = SplayAz.ar(2, snd2);
      snd2 = LPF.ar(snd2, LinExp.kr(LFNoise2.kr(1),-1,1,2500,3000));
      5.do{snd2 = AllpassN.ar(snd2, 0.1, {Rand(0.01,0.099)}!2, 3)};
      snd2 = LPF.ar(snd2, LinExp.kr(LFNoise2.kr(1),-1,1,2500,3000));
      snd2 = LeakDC.ar(snd2);
      snd = SelectX.ar(0.6,[snd,snd2]);
      ReplaceOut.ar(0,snd * 6.neg.dbamp);
   }).add;

   Routine {
      // sync the server
      s.sync;
      // https://www.johndcook.com/blog/2018/06/12/mathematics-of-deep-note/
      // D1, D2, A2, D3, A3, D4, A4, D5, A5, D6, and F#6
      ~thxNotes = [26,38,45,50,57,62,69,74,81,86,90];
      // play all notes
      Synth.tail(s,&#34;out&#34;);
      ~thxNotes.do({ arg v;
         if (v&lt;60,{
            // 2 voices
            Synth(&#34;thx&#34;,[\noteFinal,v]);
            Synth(&#34;thx&#34;,[\noteFinal,v]);
         },{
            // 3 voices, slightly detuneds
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
            Synth(&#34;thx&#34;,[\noteFinal,v+rrand(-0.1,0.1)]);
         });
      });


   }.play;
});
)
</code></pre><p>best results if you have a subwoofer :)</p>





            </div></div>
  </body>
</html>
