<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>James Routley | Feed</title>
    <link
      rel="stylesheet"
      type="text/css"
      href="../styles.css"
      media="screen"
    />
  </head>
  <body>
    <a href="/index.html">Back</a>
    <a href="https://wiki.tcl-lang.org/page/Implement&#43;tcl&#43;in&#43;tcl">Original</a>
    <h1>Implement Tcl in Tcl (2007)</h1>
    
    <div id="readability-page-1" class="page"><div>
<div>
<p>Maybe someone has already done this. But the question in my mind is how much of Tcl can be implimented in Tcl itself. Obviously you can&#39;t implement system calls in Tcl, but you could implement just about everthing else. What commands/parts of Tcl would be in the minimal set? <a href="https://wiki.tcl-lang.org/page/Earl+Johnson">Earl Johnson</a></p><ul><li>Minimal set</li></ul><ol><li>&#34;set&#34; both scalar and array modes.</li><li>&#34;eval&#34; command</li><li>&#34;unknown&#34; </li><li>&#34;string index&#34; command</li><li>&#34;string length&#34; command</li><li>&#34;lindex&#34; command</li><li>&#34;llength&#34; command</li><li>&#34;list&#34; command</li><li>what else???</li></ol><ul><li>Things that could be simulated in Tcl (theoretically)</li></ul><ol><li>interpreters (design?). </li><li>namespaces (design?). </li><li>expr (design?). </li><li>callstack (design?) .</li><li>proc (design?) .</li><li>if/while/switch/for/foreach.</li><li>regexp (sounds hard) .</li><li>package system (design?) .</li><li>traces (design?).</li><li>event loop (design?).</li><li>glob (design?).</li></ol><ul><li>What I expect to be hard</li></ul><ol><li>expr</li><li>regexp <i>(<a href="https://wiki.tcl-lang.org/page/Lars+H">Lars H</a>: You get a bit of the way with <a href="https://wiki.tcl-lang.org/page/grammar%5Ffa">grammar_fa</a>.)</i></li></ol><ul><li>What I don&#39;t expect to be done</li></ul><ol><li>C API</li><li>Things that need system calls </li></ol><ul><li>Other languages are self hosting like this</li></ul><ol><li>C =&gt; c compiler</li><li>Python =&gt; PyPy</li><li>Smalltalk </li><li>Others??</li></ol><p>Of course I expect that this TclTcl would be very slow, but interesting, and very easy to port to embedded systems.</p><p><a href="https://wiki.tcl-lang.org/page/SS">SS</a> 12Oct2004: One day for fun I implemented a subset of Tcl in Tcl, with some more work may run non trivial programs. Consider this program BSD-licensed.</p><pre> # Lct - A Tcl-like language implemented in Tcl
 # The implementation does NOT try to exploit the fact we
 # are implementing Tcl in Tcl exposing some Tcl built-in
 # to make the work simpler, thus this implementation is
 # quite portable. It should be quite easy to port it
 # to Python, PHP, Perl, Scheme and alike.
 #
 # Copyright (C) 2003 Salvatore Sanfilippo
 #
 # Biggest differences with Tcl:
 #
 # A lot of course, but this are important stuff you should
 # know before to try even some trivial example:
 #
 # - No &#39;expr&#39; support, this also changes if/for/while semantic:
 #   What in Tcl is &#34;if {$a &gt; $b} ...&#34; here is &#34;if {&gt; $a $b} ...&#34; and so on.
 #   Math is done lispwise using +, -, *, / commands and so on.
 #   Note that conditionals doesn&#39;t perform substitution also, so
 #   There is no need of [] in most cases, but you need to use
 #   the &#34;pass&#34; identity function to test for a variable, like:
 #   if {pass $test} {...} elseif {...} else {...}
 # - No arrays for now
 # - No namespaces
 # - False is both zero and an empty string, no implicit expr in
 #   conditionals means to face the need to handle any kind of return
 #   string as false or true.
 # - True is just what&#39;s not false ;) any string that isn&#39;t &#34;0&#34; nor &#34;&#34;.
 # - The &#39;pass&#39; command is the &#34;identity&#34; command returning it&#39;s only argument.
 #   pass foobar ;# =&gt; foobar
 # - Unbalanced {} in comments are not a problem (but you need to quote
 #   anyway if your Lct program body is defined inside a Tcl script).
 #
 # TODO:
 #
 # - variable number of arguments to procedures
 # - exceptions, i.e. [catch]
 # - upvar
 # - break/for
 # - uproc foo args body, foo will be executed in the caller context
 # - set x(foo), $x(foo) as syntax glue for some &#34;dict set&#34; &#34;dict get&#34; command
 # - be able to save the continuation may be cool, but this interpreter
 #   calls itself from Tcl, so I will remove the recursion or figure how
 #   to reenter the recursion starting from state-information.
 # - handle line numbers in the parser to specify error&#39;s line number
 # - check for raised error condition in LctEval to exit with an error

 ################################################################################
 #                                 Lct Core
 ################################################################################

 # Interpreter state is implemented as global vars and arrays
 # no support for multiple interpreters (for now at least).
 array set ::LctCommands {}
 array set ::LctProcs {}
 set ::LctStack {}
 set ::LctError {}
 set ::LctStackLen 0

 proc LctAddStackFrame {} {
     incr ::LctStackLen
     namespace eval StackFrame$::LctStackLen {
        variable EvalLevel 0
        array set Locals {}
        array set Globals {}
     }
 }

 proc LctRemoveStackFrame {} {
     namespace delete StackFrame$::LctStackLen
     incr ::LctStackLen -1
 }

 proc LctSaveStackFrame varname {
     upvar $varname saved
     set sf $::LctStackLen
     set saved {}
     lappend saved [array get StackFrame$sf\::Locals]
     lappend saved [array get StackFrame$sf\::Globals]
     lappend saved [set StackFrame$sf\::EvalLevel]
 }

 proc LctRestoreStackFrame varname {
     upvar $varname saved
     set sf $::LctStackLen
     namespace eval StackFrame$sf {}
     array set StackFrame$sf\::Locals [lindex $saved 0]
     array set StackFrame$sf\::Globals [lindex $saved 1]
     set StackFrame$sf\::EvalLevel [lindex $saved 2]
 }

 # Create the top-level stack frame
 LctAddStackFrame

 # The parser is the core of the interpreter, being this
 # interpreted from the source code directly. No compilation.
 proc LctParser {text tokenvar indexvar {dosubst 0}} {
     upvar $tokenvar token
     upvar $indexvar i
     set token {}
     set inside {}
     set dontstop $dosubst
     while 1 {
        # skip spaces
        while {!$dontstop &amp;&amp; [string match &#34;\[ \t\]&#34; [string index $text $i]]} {
            incr i
        }
        # skip comments
        if {!$dontstop &amp;&amp; [string equal [string index $text $i] #]} {
            while {[string length [string index $text $i]] &amp;&amp;
                  ![string match [string index $text $i] \n]} \
            {
                incr i
            }
        }
        # check for special conditions
        if {!$dontstop} {
            switch -exact -- [string index $text $i] {
                {} {return EOF}
                {;} -
                &#34;\n&#34; {incr i; return EOL}
            }
        }
        # main parser loop
        while 1 {
            switch -exact -- [string index $text $i] {
                {} break
                { } -
                &#34;\t&#34; -
                &#34;\n&#34; -
                &#34;;&#34; {
                    if {!$dontstop} {
                        break;
                    }
                }
                \&#34; {
                    if {[string equal $inside {}]} {
                        incr dontstop
                        set inside \&#34;
                        incr i
                        continue
                    } elseif {[string equal $inside \&#34;]} {
                        incr dontstop -1
                        set inside {}
                        incr i
                        continue
                    }
                }
                &#34;\{&#34; {
                    if {[string equal $inside {}]} {
                        incr dontstop
                        set inside &#34;\{&#34;
                        incr i
                        continue
                    } elseif {[string equal $inside &#34;\{&#34;]} {
                        incr dontstop
                    }
                }
                &#34;\}&#34; {
                    if {[string equal $inside &#34;\{&#34;]} {
                        incr dontstop -1
                        if {$dontstop == 0} {
                            set inside {}
                            incr i
                            continue
                        }
                    }
                }
                \$ {
                    if {![string equal $inside &#34;\{&#34;]} {
                        if {![string equal [string index $text [expr {$i+1}]] $]} {
                            set res [LctSubstVar $text $indexvar]
                            append token $res
                            continue
                        }
                    }
                }
                \[ {
                    if {![string equal $inside &#34;\{&#34;]} {
                        set res [LctSubstCmd $text $indexvar]
                        append token $res
                        continue
                    }
                }
            }
            append token [string index $text $i]
            incr i
        }
        return TOK
     }
 }

 proc LctSubstCmd {text indexvar} {
     upvar $indexvar i
     set go 1
     set cmd {}
     incr i
     while {$go} {
        switch -exact -- [string index $text $i] {
            {} break
            \[ {incr go}
            \] {incr go -1}
        }
        append cmd [string index $text $i]
        incr i
     }
     set cmd [string range $cmd 0 end-1]
     return [LctEval $cmd]
 }

 # Get the control when a &#39;$&#39; (not followed by $) is encountered,
 # extract the name of the variable, and return its content.
 proc LctSubstVar {text indexvar} {
     upvar $indexvar i
     set dontstop 0
     set varname {}
     incr i
     while {1} {
        switch -exact -- [string index $text $i] {
            \[ -
            \] -
            &#34;\t&#34; -
            &#34;\n&#34; -
            &#34;\&#34;&#34; -
            \; -
            \{ -
            \} -
            \$ -
            ( -
            ) -
            { } -
            {} {
                if {!$dontstop} {
                    break
                }
            }
            ( {incr dontstop}
            ) {incr dontstop -1}
            default {
                append varname [string index $text $i]
            }
        }
        incr i
     }
     if {![LctLookupVar $varname content]} {
        error &#34;No such variable &#39;$varname&#39;&#34;
     } else {
        return $content
     }
 }

 proc LctLookupVar {varname contentvar} {
     set sf $::LctStackLen
     upvar $contentvar content
     if {[info exists StackFrame$sf\::Globals($varname)]} {
        set sf 1
     }
     if {![info exists StackFrame$sf\::Locals($varname)]} {
        return 0
     }
     set content [set StackFrame$sf\::Locals($varname)]
     return 1
 }

 proc LctGetEvalLevel {} {
     return [set StackFrame$::LctStackLen\::EvalLevel]
 }

 proc LctSetEvalLevel newlevel {
     set StackFrame$::LctStackLen\::EvalLevel $newlevel
 }

 proc LctEval script {
     set result {}
     set eof 0
     set i 0

     set level [LctSetEvalLevel [expr {[LctGetEvalLevel]+1}]]
     while {!$eof &amp;&amp; ([LctGetEvalLevel] &gt;= $level)} {
        set argv {}
        set argc 0
        while 1 {
            set state [LctParser $script token i]
            if {[string equal $state EOF]} {
                set eof 1
            }
            switch $state {
                EOF -
                EOL break
                default {
                    lappend argv $token
                    incr argc
                }
            }
        }
        if {$argc} {
            set cmd [lindex $argv 0]
            if {![info exists ::LctCommands($cmd)]} {
                error &#34;No such command &#39;$cmd&#39;&#34;
            } else {
                set result [$::LctCommands($cmd) $argv]
                if {[string length $::LctError]} {error &#34;$::LctError\n  in script:\n$script&#34;}
            }
        }
     }
     if {$level == [LctGetEvalLevel]} {
        LctSetEvalLevel [expr {[LctGetEvalLevel]-1}]
     }
     return $result
 }

 proc LctUplevel {level script result} {
     upvar $result res
     if {$::LctStackLen &lt;= $level} {
        LctSetError &#34;Bad Level&#34;
        return
     }
     LctSaveStackFrame stackframe
     incr ::LctStackLen -$level
     set res [LctEval $script]
     incr ::LctStackLen $level
     LctRestoreStackFrame stackframe
     return $res
 }

 # Do substitution of commands and vars
 proc LctSubst string {
     set i 0
     set s [LctParser $string token i 1]
     return $token
 }

 proc LctRegisterCommand {name function} {
     set ::LctCommands($name) $function
 }

 proc LctSetVar {varname value} {
     set sf $::LctStackLen
     if {[info exists StackFrame$sf\::Globals($varname)]} {
        set sf 1
     }
     return [set StackFrame$sf\::Locals($varname) $value]
 }

 proc LctMarkGlobal varname {
     set sf $::LctStackLen
     set StackFrame$sf\::Globals($varname) {}
 }

 proc LctSetError error {
     set ::LctError $error
 }

 # In Lct both 0 and empty string is false.
 proc LctIsFalse value {
     if {[string equal $value 0] || [string equal $value {}]} {
        return 1
     } else {
        return 0
     }
 }

 # Define it as the negation of LctIsFalse
 proc LctIsTrue value {
     return [expr {![LctIsFalse $value]}]
 }

 ################################################################################
 #                                Core Commands
 ################################################################################

 proc LctSet argv {
     if {[llength $argv] != 3} {
        LctSetError &#34;Bad number of arguments, try: set varname value&#34;
        return
     }
     return [LctSetVar [lindex $argv 1] [lindex $argv 2]]
 }

 proc LctPut argv {
     set nonewline 0
     if {[llength $argv] &gt;= 2 &amp;&amp; [string match [lindex $argv 1] -nonewline]} {
        set nonewline 1
        set argv [lrange $argv 1 end]
     }
     if {[llength $argv] != 2} {
        LctSetError &#34;Bad number of arguments, try: put string&#34;
        return
     }
     puts -nonewline stdout [lindex $argv 1]
     if {!$nonewline} {
        puts {}
     }
     return {}
 }

 # That&#39;s a generic binding for math stuff. It uses expr, and
 # &#39;sens&#39; what operator to use from the name of the procedure itself.
 proc LctGenericMathOp argv {
     if {[llength $argv] != 3} {
        LctSetError &#34;Bad number of arguments, try: + number number&#34;
        return
     }
     set e [lindex $argv 1][lindex $argv 0][lindex $argv 2]
     set e [string map &#34;\[ \\\[ \] \\\]&#34; $e]
     return [expr $e]
 }

 proc LctIncr argv {
     if {[llength $argv] != 2 &amp;&amp; [llength $argv] != 3} {
        LctSetError &#34;Bad number of arguments, try: incr varname ?increment?&#34;
        return
     }
     set varname [lindex $argv 1]
     if {[llength $argv] == 3} {
        set increment [lindex $argv 2]
     } else {
        set increment 1
     }
     if {![LctLookupVar $varname val]} {
        LctSetError &#34;No such var &#39;$varname&#39;&#34;
        return
     }
     if {[catch {expr {$val+$increment}} result]} {
        LctSetError &#34;Expected integer, got something else ($result)&#34;
        return
     }
     return [LctSetVar $varname $result]
 }

 proc LctProc argv {
     if {[llength $argv] != 4} {
        LctSetError &#34;Bad number of arguments, try: proc name args body&#34;
        return
     }
     LctRegisterCommand [lindex $argv 1] LctCallProc
     set ::LctProcs([lindex $argv 1]) [list [lindex $argv 2] [lindex $argv 3]]
     return {}
 }

 # This built-in is used to call user-defined procedures
 # It checks for argv(0} in order to get the name of the
 # procedure to call, then create a new stack frame and call it.
 proc LctCallProc argv {
     foreach {arglist body} $::LctProcs([lindex $argv 0]) break
     if {[llength $argv]-1 != [llength $arglist]} {
        LctSetError &#34;Wrong number of args calling procedure &#39;[lindex $argv 0]&#39;&#34;
        return
     }
     set l [llength $arglist]
     LctAddStackFrame
     for {set i 0} {$i &lt; $l} {incr i} {
        LctSetVar [lindex $arglist $i] [lindex $argv [expr {$i+1}]]
     }
     set result [LctEval $body]
     LctRemoveStackFrame
     return $result
 }

 # Return is simple, we set the stack frame&#39;s EvalLevel to 0 in order
 # to be sure eval will return to the previous procedure.
 proc LctReturn argv {
     if {[llength $argv] != 1 &amp;&amp; [llength $argv] != 2} {
        LctSetError &#34;Bad number of arguments, try: return ?value?&#34;
        return
     }
     LctSetEvalLevel 0
     if {[llength $argv] == 2} {
        return [lindex $argv 1]
     } else {
        return {}
     }
 }

 # Facility to pop arguments in varargs proc.
 proc LctPopArg varname {
     upvar $varname argv
     set arg [lindex $argv 0]
     set argv [lreplace $argv 0 0]
     return $arg
 }

 # The if command implemented as trivial FSA.
 proc LctIf argv {
     set argv [lreplace $argv 0 0] ;# Drop the &#39;if&#39; first argument.
     set state EXPR
     while {[llength $argv]} {
        switch -exact $state {
            EXPR {
                set e [LctPopArg argv]
                set res [LctIsTrue [LctEval $e]]
                if {$res} {
                    set state EVAL_NEXT
                } else {
                    set state SKIP_TRUE_BRANCH
                }
            }
            EVAL_NEXT {
                set script [LctPopArg argv]
                return [LctEval $script]
            }
            SKIP_TRUE_BRANCH {
                LctPopArg argv ;# Just skip it
                set state ELSE_OR_ELSEIF_OR_FALSE_BRANCH
            }
            ELSE_OR_ELSEIF_OR_FALSE_BRANCH {
                set x [LctPopArg argv]
                if {[string equal $x else]} {
                    set state EVAL_NEXT
                } elseif {[string equal $x elseif]} {
                    set state EXPR
                } else {
                    return [LctEval $x]
                }
            }
        }
     }
     switch -exact $state {
        EXPR {
            LctSetError &#34;Missing expression in if&#34;
            return
        }
        EVAL_NEXT {
            LctSetError &#34;Missing script in if&#34;
            return
        }
     }
 }

 proc LctWhile argv {
     if {[llength $argv] != 3} {
        LctSetError &#34;Bad number of arguments, try: while cond body&#34;
        return
     }
     foreach {_ cond body} $argv break
     while {[LctIsTrue [LctEval $cond]]} {
        set result [LctEval $body]
     }
     return $result
 }

 proc LctPass argv {
     if {[llength $argv] != 2} {
        LctSetError &#34;Bad number of arguments, try: pass ?value?&#34;
        return
     }
     return [lindex $argv 1]
 }

 proc LctEvalCmd argv {
     set script {}
     foreach x [lrange $argv 1 end] {append script $x}
     return [LctEval $script]
 }

 proc LctUplevelCmd argv {
     if {[llength $argv] &lt; 2} {
        LctSetError &#34;Bad number of arguments, try: uplevel ?level? arg ... ?arg?&#34;
        return {}
     }
     foreach {- level script} $argv break
     LctUplevel $level $script result
     return $result
 }

 proc LctGlobalCmd argv {
     set argv [lrange $argv 1 end]
     foreach varname $argv {
        LctMarkGlobal $varname
     }
     return {}
 }

 # Sort part of the core
 LctRegisterCommand proc LctCallProc
 LctRegisterCommand set LctSet
 LctRegisterCommand proc LctProc
 LctRegisterCommand return LctReturn
 LctRegisterCommand if LctIf
 LctRegisterCommand pass LctPass
 LctRegisterCommand while LctWhile
 LctRegisterCommand eval LctEvalCmd
 LctRegisterCommand uplevel LctUplevelCmd
 LctRegisterCommand global LctGlobalCmd

 # Random stuff that are really needed, but not part of the core itself.
 LctRegisterCommand puts LctPut
 LctRegisterCommand + LctGenericMathOp
 LctRegisterCommand - LctGenericMathOp
 LctRegisterCommand * LctGenericMathOp
 LctRegisterCommand / LctGenericMathOp
 LctRegisterCommand % LctGenericMathOp
 LctRegisterCommand &gt; LctGenericMathOp
 LctRegisterCommand &gt;= LctGenericMathOp
 LctRegisterCommand &lt; LctGenericMathOp
 LctRegisterCommand =&lt; LctGenericMathOp
 LctRegisterCommand == LctGenericMathOp
 LctRegisterCommand != LctGenericMathOp
 LctRegisterCommand incr LctIncr

 ################################################################################
 #                                   Example
 ################################################################################

 set text {
     # Test for comment
     # Test basic substitution
     set name &#34;All The Tclers&#34;
     puts &#34;Hello to $name [+ 1 2] [+ 10 20]&#34;
     puts &#34;(6*3)+(8*2)=[+ [* 6 3] [* 8 2]]&#34;
     # Test procedures
     proc test a {
        puts &#34;I&#39;m printing $a&#34;
     }
     test &#34;Hello World&#34;
     # Test return from procedure without value
     proc testreturn {} {
        puts &#34;Test Return&#34;
        return
        puts &#34;FooBar&#34;
     }
     testreturn
     testreturn
     # Test return with value
     proc testreturn2 x {
        return $x$x
     }
     puts &#34;Return With Value: [testreturn2 XyZ]&#34;
     # Test conditionals
     set a 1
     if {pass $a} {puts &#34;($a) is true&#34;} else {puts &#34;($a) is false&#34;}
     set a 0
     if {pass $a} {puts &#34;($a) is true&#34;} else {puts &#34;($a) is false&#34;}
     set a foobar
     if {pass $a} {puts &#34;($a) is true&#34;} else {puts &#34;($a) is false&#34;}
     set a {}
     if {pass $a} {puts &#34;($a) is true&#34;} else {puts &#34;($a) is false&#34;}
     # Test incr
     set x 10
     puts &#34;Now it is $x&#34;
     incr x
     puts &#34;Now it is $x&#34;
     incr x -2
     puts &#34;Now it is $x&#34;
     # Test Loops
     while {&lt; $x 20} {
        incr x 1
        puts &#34;Hello World $x&#34;
     }
     set script {puts -nonewline we;}
     eval $script {puts &#34; are inside eval&#34;}
     # Test global
     proc testglobal {} {
        global x
        puts &#34;x is global, value: $x&#34;
        set x 30
        puts &#34;x value changed, will print the new value outside the proc&#34;
     }
     testglobal
     puts &#34;x new value is $x&#34;
     # Test uplevel
     proc testuplevel {} {
        puts ---
        uplevel 1 {puts &#34;testglobal was called from uplevel: [testglobal]&#34;}
        uplevel 1 {puts &#34;In uplevel, x = $x&#34;}
     }
     testuplevel
 }

 LctEval $text</pre><hr/><p>I have got problems testing it (error: invalid command name &#34;::macro::parser&#34;).</p><p>This is because the call to ::macro::parser doesn&#39;t reference any known function. When I changed the call to LctParser, everything worked for me. I have taken the liberty of making the change in the code above. -RL</p><p>I suppose also that the parser would have problems with some special ugly code as follow, which is accepted by the original tcl parser:</p><pre>   set {]} t
   set a [list ${]} ]
   set {&#34;} t
   set a &#34;a ${&#34;} &#34;
   set a &#34;ewe [list &#34;sewe&#34;] ewr&#34;</pre><p>Anyway programming tcl in tcl is very interesting idea if one wants to test experimental tcl interpreters or build in new functions which are not supported by original interpreter</p><p><a href="https://wiki.tcl-lang.org/page/Lars+H">Lars H</a>: I think several of those would actually be handled OK, but</p><pre>   set a [list ${]} ]</pre><p>is not (the only extra rule in command substitution nesting is that &#39;]&#39; terminates a command in places where &#39;;&#39; would, so brackets need not nest properly as LctSubstCmd assumes). Backslash substitution is missing entirely. Also the following isn&#39;t handled correctly:</p><pre>   set a x{$a}x</pre><p>The left brace only has special powers if it is the first character in a word. What worries me most about the above parser is however:</p><ul><li>its use of a variable $inside (that is too simple to keep track of the state of a correct Tcl parser), and</li><li>the one big loop over characters in the input (hard to analyse, because there are so many cases to consider, and thus likely to buggy).</li></ul><p>For a more complete Tcl parser, available here on this Wiki, see <a href="https://wiki.tcl-lang.org/page/parsetcl">parsetcl</a>.</p><hr/><p><a href="https://wiki.tcl-lang.org/page/Category+Application">Category Application</a> - <a href="https://wiki.tcl-lang.org/page/Category+Tcl+Implementations">Category Tcl Implementations</a></p>
</div>
</div></div>
  </body>
</html>
